//===== Cronus Script ========================================
//= iRO Evento do Dia dos Namorados (2009)
//===== Por: =================================================
//= Kisuka
//===== Versão Atual: ========================================
//= 1.2b
//===== Compativél com: ======================================
//= Cronus SVN
//===== Descrição: ===========================================
//= iRO Evento do Dia dos Namorados. (2009)
//= Cria Chocolate/Caixa de Chocolate/Chocolate feito em casa.
//= Cria Anéis/Caixas do Dia dos Namorados.
//= Troca de anéis com o sexo oposto.
//= Registra anéis para obter votos.
//=
//= O homem e a mulher com a maioria dos votos no final do
//= evento receberá o item (14466) da equipe de GM.
//=
//= Deve permitir os itens do evento no item_db2 e item_trade!
//===== Comentários Adicionais: ==============================
//= 1.0 Primeira Versão. [Kisuka]
//= 1.1-1.2 Corrigido exploração de anéis ilimitados e não deletar choco [Lupus] 
//= 1.2a - Traduzido [Evanio]
//= 1.2b - Revisado Script [Asus]
//============================================================

// Marco Bassinio (Cria Chocolate/Caixa de Chocolate)
prontera,164,174,4	script	Vendedor#Val09	58,{
	mes "[Marco Bassinio]";
	mes "Ei, aqui está algo que não se vê todos os dias!";
	mes "Algo que você nunca poderá encontrar em Rune-Midgarts!";
	mes "Algo que lhe faz feliz em apenas uma mordida!";
	next;
	mes "[Marco Bassinio]";
	mes "É a sobremesa pefeita e o presente perfeito para seus entes queridos.";
	mes "Um chocolate tradicional e caseiro de alta qualidade, custa apenas 5000z cada um!";
	next;
	switch(select("Eu quero um, por favor!:Eu quero quebrar o chocolate!:Fim de troca.")) {
		case 1:
			mes "[Marco Bassinio]";
			mes "Ahaha, meu caro.";
			mes "Este chocolate não se";
			mes "compara com os outros.";
			mes "Cada peça tem a";
			mes "devoção da pessoa";
			mes "que o fez!";
			next;
			mes "[Marco Bassinio]";
			mes "Então, é por isso que não posso vender";
			mes "mais do que 5 deles a cada uma hora.";
			mes "Se você realmente quiser mais,";
			mes "fale comigo depois.";
			mes "Quntos você quer?";
			next;
			while(.@input <= 0 || .@input > 5) {
				input .@input;
				if (.@input < 1) {
					mes "[Marco Bassinio]";
					mes "Oh, é uma vergonha!";
					mes "eu tenho certeza que você vai se arrepender";
					mes "depois, perder esta oportunidade";
					mes "de não compra-lo...";
					next;
					mes "[Marco Bassinio]";
					mes "Lembre-se, você nunca";
					mes "irá enontrar esta qualidade em nenhum lugar!";
					mes "Volte depois,";
					mes "caso mude de ideia.";
					close;
				}
				if (.@input > 5) {
					mes "[Marco Bassinio]";
					mes "Ugh.. eu não falei a você?";
					mes "5 é o maximo!";
					mes "Eu não posso vender mais";
					mes "para a mesma pessoa.";
					next;
					mes "[Marco Bassinio]";
					mes "E Você sabe que não é";
					mes "como uma refeição diária.";
					mes "Comer demais não é";
					mes "realmente bom para você.";
					next;
				}
			}
			if (!checkweight(558, .@input)) {
				mes "[Marco Bassinio]";
				mes "Você está carregando muitos itens.";
				mes "Por favor, utilize os Serviços da Kafra.";
				close;
			}
			set .@price, .@input * 5000;
			if (Zeny < .@price) {
				mes "[Marco Bassinio]";
				mes "Parece que você não tem";
				mes "Zeny suficiente com você.";
				mes "Talvez você deveria pedir";
				mes "emprestado à algum amigo.";
				mes "Porque, eu não vou";
				mes "está aqui todos os dias.";
				close;
			}else{
				mes "[Marco Bassinio]";
				mes "Bom para você!";
				mes "Tembém é perfeito como um presente!";
				mes "Você sabe que não pode fazer este";
				mes "tipo de chocolate normalmente.";
				next;
				mes "[Marco Bassinio]";
				mes "Se você quiser mais,";
				mes "você terá que voltar depois.";
				mes "Pode ser um boa idéia";
				mes "comprar mais depois de um tempo";
				mes "você tem uma chance...!";
				set Zeny,Zeny - .@price;
				getitem 558,.@input;
				close;
			}
		case 2:
			mes "[Marco Bassinio]";
			mes "Se você quiser quebrar";
			mes "o chocolate, é claro,";
			mes "você vai precisar de Chocolate,";
			mes "Papel de presente,";
			mes "Laço para presente, e uma caixa.";
			next;
			mes "[Marco Bassinio]";
			mes "Você também precisa pagar";
			mes "500 zeny para cravar seu";
			mes "nome na caixa.";
			mes "Está tudo preparado...?";
			next;
			if(countitem(7175) < 1 || countitem(7174) < 1 || countitem(7948) < 1 || Zeny < 500) {
				mes "[Marco Bassinio]";
				mes "Hmm.. Parece que você não";
				mes "tem o material sufuciente";
				mes "para decorar a caixa ...";
				mes "Você não pode apenas colocar o seu presente";
				mes "em um simples olhar na caixa..";
				mes "Você não acha?";
				next;
				mes "[Marco Bassinio]";
				mes "Você precisa trazer";
				mes "Papel de presente,";
				mes "Laço para presente, e uma Caixa.";
				mes "Oh, também 500 zeny,";
				mes "e não esqueça de levar o seu";
				mes "Amor verdadeiro contigo!!";
				close;
			}
			if(countitem(558) < 1) {
				mes "[Marco Bassinio]";
				mes "Ei, olhe, aventureiro!";
				mes "Não posso criar algo imediatamente!";
				mes "Eu não sou um alquimista ou algo do gênero.";
				next;
				mes "[Marco Bassinio]";
				mes "Você não vai querer";
				mes "uma caixa de chocolate vazia";
				mes "sem nenhum chocolate dentro,";
				mes "estou certo?";
				close;				
			}
			if (!checkweight(12744,1)) {
				mes "[Marco Bassinio]";
				mes "Você está carregando muitos itens.";
				mes "Por favor use os Serviços da Kafra.";
				close;
			}
			mes "[Marco Bassinio]";
			mes "Aqui, olha!";
			mes "Esta é a sua caixa de chocolate";
			mes "com seu nome cravado nela.";
			mes "Não é fabuloso?";
			mes "Veja, o seu nome";
			mes "enorme na caixa!";
			next;
			delitem 558,1;
			delitem 7175,1;
			delitem 7174,1;
			delitem 7948,1;
			set Zeny,Zeny-500;
			getnameditem 12744,strcharinfo(0);
			mes "[Marco Bassinio]";
			mes "Feliz Dia dos Namorados!";
			mes "Dia dos Namorados e o motivo de eu ter voltado.";
			close;
		case 3:
			mes "[Marco Bassinio]";
			mes "Oh, é uma vergonha!";
			mes "eu tenho certeza que você vai se arrepender";
			mes "depois, perder esta oportunidade";
			mes "de não compra-lo...";
			next;
			mes "[Marco Bassinio]";
			mes "Lembre-se, você nunca";
			mes "irá enontrar esta qualidade em nenhum lugar!";
			mes "Volte depois,";
			mes "caso mude de ideia.";
			close;
	}
}

// Negociador de Pacotes (Vende Papel de Presente, Laço, e Caixa)
prontera,147,171,5	script	Vendedor Embrulho#Val09	58,{
	mes "[Vendedor de Embrulho]";
	mes "Olá.";
	mes "Eu sou um vendedor de Embrulhos,";
	mes "Eu vendo caixas de papel e";
	mes "material para embalagem de presentes.";
	next;
	while(1) {
		mes "[Vendedor de Embrulho]";
		mes "Você quer comprar algo?";
		next;
		switch(select("Papel de presente:Laço para presente:Caixa:Cancelar.")) {
			case 1:
				mes "[Vendedor de Embrulho]";
				mes "Cada 200 zeny para";
				mes "1 Papel de presente.";
				mes "Quantos você quer?";
				mes "Você não pode comprar";
				mes "mais de 10 itens de uma vez só.";
				next;
				input .@input;
				if (.@input <= 0) {
					mes "[Vendedor de Embrulho]";
					mes "Nada de compra.";
					mes "Volte quando";
					mes "precisar de algo.";
					close;
				}
				if (.@input > 10) {
					mes "[Vendedor de Embrulho]";
					mes "Eu lhe falei para não comprar mais que 10...";
					next;
					break;
				}
				if (!checkweight(7175,.@input)) {
					mes "[Vendedor de Embrulho]";
					mes "Você esta carregando muitos itens.";
					mes "Por favor use os Serviços da Kafra.";
					close;
				}
				set .@price, .@input * 200;
				if (Zeny < .@price) {
					mes "[Vendedor de Embrulho]";
					mes "Você não tem dinheiro suficiente.";
					mes "Por favor, verifique sua carteira.";
					next;
					break;
				}else{
					mes "[Vendedor de Embrulho]";
					mes "Aqui está.";
					mes "Espero que torne seu";
					mes "Dias dos Namorados";
					mes "mais agradável!";
					set Zeny,Zeny-.@price;
					getitem 7175,.@input;
					next;
					break;
				}
			case 2:
				mes "[Vendedor de Embrulho]";
				mes "São 200 zeny para";
				mes "1 Laço para presente.";
				mes "Quantos você quer?";
				mes "Você só pode comprar";
				mes "10 unidades por vez.";
				next;
				input .@input;
				if (.@input <= 0) {
					mes "[Vendedor de Embrulho]";
					mes "Nada de compra.";
					mes "Volte quando";
					mes "você precisar de algo.";
					close;
				}
				if (.@input > 10) {
					mes "[Vendedor de Embrulho]";
					mes "Eu falei que não vendo mais que 10 por vez...";
					next;
					break;
				}
				if (!checkweight(7174,.@input)) {
					mes "[Vendedor de Embrulho]";
					mes "Você esta carregando muitos itens.";
					mes "Por favor use os Serviços da Kafra.";
					close;
				}
				set .@price, .@input * 200;
				if (Zeny < .@price) {
					mes "[Vendedor de Embrulho]";
					mes "Você não tem dinheiro suficiente.";
					mes "Por favor, verifique sua carteira.";
					next;
					break;
				}else{
					mes "[Vendedor de Embrulho]";
					mes "Aqui está.";
					mes "Espero que torne seu";
					mes "Dias dos Namorados";
					mes "mais agradável!";
					set Zeny,Zeny-.@price;
					getitem 7174,.@input;
					next;
					break;
				}
			case 3:
				mes "[Vendedor de Embrulho]";
				mes "São 600 zeny para";
				mes "1 Caixa.";
				mes "Quantas você quer?";
				mes "Você só pode comprar";
				mes "10 unidades por vez.";
				next;
				input .@input;
				if (.@input <= 0) {
					mes "[Vendedor de Embrulho]";
					mes "Nada de compra.";
					mes "Volte quando";
					mes "você precisar de algo.";
					close;
				}
				if (.@input > 10) {
					mes "[Vendedor de Embrulho]";
					mes "Eu falei que não vendo mais que 10 por vez...";
					next;
					break;
				}
				if (!checkweight(7948,.@input)) {
					mes "[Vendedor de Embrulho]";
					mes "Você esta carregando muitos itens.";
					mes "Por favor use os Serviços da Kafra.";
					close;
				}
				set .@price, .@input * 600;
				if (Zeny < .@price) {
					mes "[Vendedor de Embrulho]";
					mes "Você não tem dinheiro suficiente.";
					mes "Por favor, verifique sua carteira.";
					next;
					break;
				}else{
					mes "[Vendedor de Embrulho]";
					mes "Aqui está.";
					mes "Espero que torne seu";
					mes "Dias dos Namorados";
					mes "mais agradável!";
					set Zeny,Zeny-.@price;
					getitem 7948,.@input;
					next;
					break;
				}	
			case 4:
				mes "[Vendedor de Embrulho]";
				mes "Adeusinho ~!";
				mes "E aproveite o seu";
				mes "Dia dos Namorados.";
				close;
		}
	}
}

// Evento de criação de anel (Cria aneis para os jogadores)
prontera,154,185,5	script	Joalheiro#Val09	721,{
	if (BaseLevel < 75) {
		mes "[Joalheiro]";
		mes "Olá, eu só crio os";
		mes "Anéis do Dia dos Namorados";
		mes "para aventureiros experientes";
		mes "Nivel 75 ou superior.";
		next;
		mes "[Joalheiro]";
		mes "Você ainda não está";
		mes "totalmente experiente.";
		mes "Volte quando você estiver";
		mes "experiente o suficiente";
		mes "para lidar com as missões.";
		close;
	}
	
	if (iROval09ring >= 1) {
		mes "[Joalheiro]";
		mes "A caixa com o anel,";
		mes "cravado o seu nome,";
		mes "é para quem você ama.";
		next;
		mes "[Joalheiro]";
		mes "O mesmo serve para os anéis";
		mes "que você receber dos outros, eles";
		mes "todos devem ser registrados";
		mes "com a Gerente de Votos.";
		mes "Ela está em pé perto";
		mes "da Fonte de Prontera.";
		next;
		mes "[Joalheiro]";
		mes "Apenas os mais populares";
		mes "homens e mulheres são";
		mes "submetidos a receber recompensas.";
		mes "Seja consciente, e sempre";
		mes "tente ficar popular!";
		close;
	}
	
	mes "[Joalheiro]";
	mes "Olá..";
	mes "Se veio até mim é porque";
	mes "está interessado no evento,";
	mes "'Criação de Anel?'";
	next;
	mes "[Joalheiro]";
	mes "Eu faço os mais preciosos";
	mes "anéis para que você possa dar";
	mes "para a pessoa amada.";
	mes "Os Anéis são muito especiais";
	mes "porque eu esculpo";
	mes "os seus nomes neles!";
	next;
	mes "[Joalheiro]";
	mes "Não é emocionante?";
	mes "Não é uma idéia brilhante?";
	mes "Consiga esse anél especial";
	mes "para seu namorado!";
	next;
	mes "[Joalheiro]";
	mes "Você deve se apressar, pois";
	mes "este evento só irá";
	mes "durar duas semanas.";
	mes "Presenteie alguém especial";
	mes "com um Anél do Dia dos Namorados.";
	next;
	mes "[Joalheiro]";
	mes "Lembre-se, você só pode";
	mes "gerar o anel uma vez.";
	mes "Você tabém vai precisar de Papel de presente,";
	mes "Laço para presente e uma Caixa";
	mes "para criar o anel.";
	next;
	mes "[Joalheiro]";
	mes "Então é melhor você ter certeza";
	mes "de quem você dará o anel.";
	mes "Pela mão de obra,";
	mes "Irá custar 1,000 zeny.";
	mes "Gostaria de fazer um anel?";
	next;
	if(select("Hmm.. Tenho que pensar em poco mais...:Certo.") == 1) {
		mes "[Joalheiro]";
		mes "Tudo bem!";
		mes "Você não pode colocar pensamentos";
		mes "negativos numa situação dessas.";
		mes "Pense sobre o que";
		mes "você realmente quer.";
		mes "Apenas siga seu coração!";
		close;
	}
	if(countitem(7175) < 1 || countitem(7174) < 1 || countitem(7948) < 1 || Zeny < 1000) {
		mes "[Joalheiro]";
		mes "Bem, Você não tem";
		mes "material suficiente";
		mes "para criar uma caixa de presente.";
		mes "Verifique o que você tem,";
		mes "e volte mais tarde";
		mes "com todos os materiais.";
		close;
	}
	mes "[Joalheiro]";
	mes "Ok!";
	mes "Farei isso imediatamente.";
	next;
	delitem 7175,1;
	delitem 7174,1;
	delitem 7948,1;
	set Zeny,Zeny-1000;
	set iROval09ring,1;
	if (Sex) {
		getnameditem 12742,strcharinfo(0);
	}else{
		getnameditem 12743,strcharinfo(0);
	}
	mes "[Joalheiro]";
	mes "Aqui, o mais precioso";
	mes "anel do mundo!";
	mes "Não esqueça, você nunca pode";
	mes "fazer este anel novamente.";
	next;
	mes "[Joalheiro]";
	mes "Você deve escolher uma pessoa";
	mes "que você realmente ama,";
	mes "e dar esse anel";
	mes "com carinho.";
	next;
	mes "[Joalheiro]";
	mes "Naturalmente, você tem que";
	mes "conseguir anéis como retorno,";
	mes "dessa maneira que você pode";
	mes "participar da votação, entendeu?";
	mes "Desafie-se a torna-se";
	mes "Valentine's Hottest!";
	close;	
}

// Gerente de Votos do Dia dos Namorados (Registros de votos)
prontera,157,185,4	script	Gerente de Votos#v	113,{
	mes "[Gerente de Votos]";
	mes "Olá, Eu sou o Gerente de votos";
	mes "do Dia dos Namorados.";
	mes "Eu estou encarregado de recolher";
	mes "anéis para este evento!";
	next;
	mes "[Gerente de Votos]";
	mes "Eu registro os anéis";
	mes "que você recebe dos outros";
	mes "e eu calculo a";
	mes "numeração total dos anéis.";
	next;
	mes "[Gerente de Votos]";
	mes "Você sabe o que eu faço além de";
	mes "apenas contar os anéis?";
	mes "Eu posso lhe dizer o nome do aventureiro";
	mes "que conseguil mais";
	mes "numeros de votos.";
	next;
	mes "[Gerente de Votos]";
	mes "Além disso, você também pode descobrir como";
	mes "ele conseguiu muitos votos.";
	next;
	while (1) {
		mes "[Gerente de Votos]";
		mes "Então, o que você quer?";
		next;
		switch(select("Por favor, registre meus anéis.:Por favor, conte meus votos.:Nada, para agora.")) {
			case 1:
				mes "[Gerente de Votos]";
				mes "Por favor me diga quantos";
				mes "anéis deseja registrar.";
				next;
				mes "[Gerente de Votos]";
				mes "Quando você escrever";
				mes "o numero de anéis,";
				mes "o numero não pode ser maior";
				mes "que o numero de anéis que";
				mes "você realmente tem.";
				mes "'0', cancela tudo.";
				next;
				input .@input;

				if (.@input <= 0) {
					mes "[Gerente de Votos]";
					mes "Você digitou 0.";
					mes "Registração cancelada.";
					next;
					break;
				}
				
				if (Sex) {
					if (countitem(7947) > .@input) {
						mes "[Gerente de Votos]";
						mes "Parece que o valor que você digitou é muito pequeno.";
						mes "Eu sei que você tem mais. Seja honesto, querido.";
						next;
						break;
					}
					
					if (countitem(7947) < .@input) {
						mes "[Gerente de Votos]";
						mes "Parece que o valor que você digitou é muito grande.";
						mes "Eu sei que você tem menos. Seja honesto, querido.";
						next;
						break;
					}
					
					mes "[Gerente de Votos]";
					mes "Eu vou levar os anéis de prata,";
					mes "e a contagem de votos para você.";
					mes "Obrigado por participar.";
					delitem 7947,.@input;
					set Val09Rings,Val09Rings+.@input;
					if (Val09Rings > $Val09votes_M) {
						set $Val09votes_M,Val09Rings;
						set $Val09name_M$,strcharinfo(0);
					}
					next;
					break;
				}else{
					if (countitem(7946) > .@input) {
						mes "[Gerente de Votos]";
						mes "Parece que o valor que você digitou é muito pequeno.";
						mes "Eu sei que você tem mais. Seja honesto, querido.";
						next;
						break;
					}
					
					if (countitem(7946) < .@input) {
						mes "[Gerente de Votos]";
						mes "Parece que o valor que você digitou é muito grande.";
						mes "Eu sei que você tem menos. Seja honesto, querido.";
						next;
						break;
					}
					
					mes "[Gerente de Votos]";
					mes "Eu vou levar os anéis de ouro,";
					mes "e a contagem de votos para você.";
					mes "Obrigado por participar.";
					delitem 7946,.@input;
					set Val09Rings,Val09Rings+.@input;
					if (Val09Rings > $Val09votes_F) {
						set $Val09votes_F,Val09Rings;
						set $Val09name_F$,strcharinfo(0);
					}
					next;
					break;
				}
			case 2:
				mes "[Gerente de Votos]";
				mes "Vamos ver...";
				mes "Você tem registrado..";
				mes ".."+Val09Rings+" anéis em média.";
				mes "e...";
				next;
				mes "[Gerente de Votos]";
				mes "O recorde atual mostra... "+$Val09name_M$+" é o lider de votos masculinos, que é registrado o total de "+$Val09votes_M+" anéis.";
				next;
				mes "[Gerente de Votos]";
				mes "O recorde atual mostra... "+$Val09name_F$+" é o lider de votos femininos, que é registrado o total de "+$Val09votes_F+" anéis.";
				next;
				break;			

			case 3:
				mes "[Gerente de Votos]";
				mes "Ei, você pode ser popular também!";
				mes "Qualquer um pode... verdade!";
				mes "Sendo que você tem que tentar muito,";
				mes "bem dificil, Mais ainda~ Hahaha!";
				close;
		}
	}
}

// Charles Orleans (Cria Chocolates feitos em casa)
prt_castle,42,35,3	script	Gerente Sobremesas#Val09	47,{
	if (Sex) {
		mes "[Charles Orleans]";
		mes "Monsieur~! O que traz";
		mes "você ao meu blo atelier?";
		mes "O que é que você quer?";
		mes "Bem, meus olhos cintilantes";
		mes "estão começando a secar e perder seu brilho";
		mes "já que você não é uma linda moça.";
		next;
		mes "[Charles Orleans]";
		mes "Por favor, deixe-me sozinho a menos";
		mes "que você tenha um negocio comigo.";
		mes "Haaaa~ Eu sou uma pessoa ocupada.";
		mes "Não me incomode....";
		mes "Chato, chato, chato~~!";
		close;
	}
	
	mes "[Charles Orleans]";
	mes "Oh, Mademoiselle!";
	mes "Neste pequeno espaço insignificante";
	mes "eu senti como se o céu";
	mes "estivesse aqui quando você entrou!";
	mes "Posso ajuda-la com";
	mes "qualquer coisa, se a senhorita desejar...";
	next;
	if(select("Por favor, faça-me um chocolate.:Não se incomode. Só estou de passagem.") == 2) {
		mes "[Charles Orleans]";
		mes "Ahhh, isto é muito triste.";
		mes "Como você pode dizer isso?";
		mes "Você é tão má.";
		mes "Não se incomode? Só estou de passagem?";
		next;
		mes "[Charles Orleans]";
		mes "O que posso fazer para você";
		mes "me der um pouco de atenção?";
		mes "Por favor.. Eu sinto como se estivesse perdido minha alma..";
		close;
	}
	if(countitem(558) < 3) {
		mes "[Charles Orleans]";
		mes "Ahhh, Mademoiselle.";
		mes "Eu não sou um alquimista,";
		mes "ou um mágico.";
		mes "Eu não posso fazer chocolate";
		mes "com o vento.";
		next;
		mes "[Charles Orleans]";
		mes "Eu sempre penso em fazer chocolate";
		mes "como uma obra de arte.";
		mes "Se você poder ver, eu não sou um cozinheiro comum...";
		mes "Eu faço chocolate com sentimento..";
		mes "mensagens de amor e coração..";
		next;
		mes "[Charles Orleans]";
		mes "Eu faço obras primas.";
		mes "Ninguém pode imitar";
		mes "a aparência e o sabor.";
		mes "Sim, não é nada";
		mes "comparado ao chocolate comum!";
		next;
		mes "[Charles Orleans]";
		mes "Eu estou envergonhado que eu não possa fazer-lo";
		mes "e provalo para você agora.";
		mes "Isto é realmente uma vergonha!";
		next;
		mes "[Charles Orleans]";
		mes "Eu realmente quero agradecer";
		mes "por você ter vindo me visitar";
		mes "e se você trazer";
		mes "^3152ff3 Chocolates^000000,";
		mes "Eu criarei um chocolate";
		mes "como você nunca viu...";
		next;
		mes "[Charles Orleans]";
		mes "Nunca provei antes de...";
		mes "Mademoiselle, com seu espirito,";
		mes "eu tenoh certeza que você pode trazer";
		mes "3 pedaços de chocolate.";
		mes "Eu não tenho nehuma duvida.";
		next;
		mes "[Charles Orleans]";
		mes "Então... O que você acha?";
		mes "Você traz ^3152ff3 Chocolates^000000?";
		mes "Eu poderia pega-los por mim mesmo,";
		mes "bmas estou ocupado com";
		mes "muito trabalho como você vê agora.";
		next;
		mes "[Charles Orleans]";
		mes "Adios, Mademoiselle.....";
		mes "Eu estarei esperando por você.";
		close;
	}else{
		if (!checkweight(559,1)) {
			mes "[Charles Orleans]";
			mes "Você está carregando muitos itens.";
			mes "Por favor, use os serviços da Kafra.";
			close;
		}
		mes "[Charles Orleans]";
		mes "Oh, Mademoiselle!";
		mes "Eu farei o melhor chocolate";
		mes "com os pedaços que você me trouxe.";
		mes "Eu vou pôr à luz dos seus olhos";
		mes "dentro desse chocolate";
		mes "e ninguém irá resistir.";
		next;
		mes "[Charles Orleans]";
		mes "Vai ser mais forte que";
		mes "uma doce poção do amor.....";
		next;
		mes "[Charles Orleans]";
		mes "Un, Deux, Trois, Quatre.....";
		mes "Assim como o patinho feio que";
		mes "que virou um belo cisnei-";
		mes "Esse ordinarios pedaços de chocolate";
		mes "tornará uma bela obra e arte!";
		next;
		mes "[Charles Orleans]";
		mes "Eles estão mudando!";
		mes "Eles estão ficando quente,";
		mes "baixinho mudando de forma,";
		mes "cada vez mais forte de novo!";
		mes "Oh, isto é uma obra prima";
		mes "que...!!";
		next;
		mes "[Charles Orleans]";
		mes "Aqui está, Mademoiselle!";
		mes "Ahahahaha, basta olhar para isso!";
		mes "EU não posso acreditar que fiz isso.";
		mes "Oh, Eu não posso desviar meus olhos..!!";
		delitem 558,3;
		getitem 559,1;
		next;
		mes "[Charles Orleans]";
		mes "Certo. Mademoiselle,";
		mes "Eu espero que isso seja";
		mes "o que você queria,";
		mes "para ostentar seu";
		mes "belo coração por dentro.";
		close;
	}
	
}