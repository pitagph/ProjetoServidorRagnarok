//===== Cronus Script ========================================
//= Tony
//===== Por: =================================================
//= Playtester, Paradox924X
//===== Versão Atual: ========================================
//= 1.3c
//===== Descrição: ===========================================
//= Negocia Projéteis
//===== Comentários Adicionais: ==============================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.3a Traduzido. [Kamper]
//= 1.3b Revisado. [RoM]
//= 1.3c Removido show, additem, digit [Yum]
//============================================================

que_ng,187,156,3	script	Negociador de Projéteis	86,{

	mes "[Tony]";
	if (BaseJob == Job_Gunslinger) {
		mes "Ei, meu nome é Tony.";
		mes "Eu sou responsável pela distribuição e fabricação";
		mes "de projéteis para Justiceiros.";
		next;
		mes "[Tony]";
		mes "Bem, grande rapaz!";
		mes "Do que você precisa?!";
		mes "Escolha o que desejar!";
		mes "E pague!";
		next;
		switch(select("Esfera Venenosa:Esfera de Chamas:Esfera de Relâmpagos:Esfera Cegante:Esfera Congelante:Cancelar")) {
			case 1:	callfunc "Bullet_Trade",937,10,13205,"Esferas Venenosas","Caninos Venenosos"; break;
			case 2:	callfunc "Bullet_Trade",7097,2,13203,"Esferas de Chamas","Corações Incandescentes"; break;
			case 3:	callfunc "Bullet_Trade",7053,3,13204,"Esferas de Relâmpagos","Cyfars"; break;
			case 4:	callfunc "Bullet_Trade",1024,5,13206,"Esferas Cegantes","Tintas de Polvo"; break;
			case 5:	callfunc "Bullet_Trade",7054,2,13207,"Esferas Congelantes","Brigans"; break;
			default:
			mes "[Tony]";
			mes "Hmm. Ok.";
			mes "Volte quando desejar!";
			mes "Eu, Tony, o Negociador de Projéteis!";
			mes "Sempre estarei aqui~!!!";
			close;
		}
	}
	mes "Ei, meu nome é Tony.";
	mes "Eu sou responsável pela distribuição e fabricação";
	mes "de projéteis para Justiceiros.";
	next;
	mes "[Tony]";
	mes "Lamento se você veio";
	mes "até aqui para comprar munição.";
	mes "Eu só posso negociar com Justiceiros credenciados.";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes "Digite a quantidade que deseja.";
	next;
	mes "[Tony]";
	mes "Eu negocio 30 "+getarg(3)+" por";
	mes "1 Fracon,";
	mes "1 Emveretarcon,";
	mes "e "+getarg(1)+" "+getarg(4)+".";
	next;
	mes "[Tony]";
	mes "O número máximo que você pode negociar é 500.";
	mes "Se você quiser cancelar, digite 0.";
	next;
	input .@amount;
	mes "[Tony]";
	if (.@amount < 1 || .@amount > 500) {
		mes "Quantidade inválida!";
		mes "Tente novamente!";
		close;
	} else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {		
			mes "Nossa... Seu inventário está realmente cheio! Volte quando ele estiver mais espaço.";
			close;
		} else {
			mes "Oh~ Ótimo!";
			mes "Número checado!";
			mes "Ai está! O que queria.";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	} else {
		mes "Tsc Tsc~";
		mes "Você não possui";
		mes "os itens que";
		mes "eu necessito.";
		mes "Prepara-os e volte.";
		close;
	}
}