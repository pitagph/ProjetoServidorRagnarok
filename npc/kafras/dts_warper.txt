//===== Cronus Script ========================================
//= Serviço de Teletransporte aos Calabouços
//===== Por: =================================================
//= Evera
//===== Versão Atual: ========================================
//= 2.4a
//===== Descrição: ===========================================
//= Sistema de votação da Corp. Kafra vs. Corp Eventos Incríveis.
//= Esse é um evento especial nos servidores oficiais onde há
//= uma grande competição entre a Corp. Kafra e a Corp. Eventos Incríveis
//= Para teletransportar aos níveis selecionados de certos calabouços.
//= NPCs do Quartel da Corp. Eventos Incríveis inclusos.
//===== Variáveis: ===========================================
//=Server - 	$dtsvote		= Variable for # of votes
//=					= Positive = Cool Corp winning
//=					= Negative = Kafra Corp winning
//=		$dts			= Variable for DTS status
//=					= (1<<0) Election
//=					= (1<<1) Cool Corp Enabled
//=					= (1<<2) Kafra Enabled
//=					= (1<<3) No winner last election
//=					= (1<<4) Cool Corp won last election
//=					= (1<<5) Kafra won last election
//=		$dtsday			= Week count for dts vote count, used for resetting players' votes
//=Character -	dtseligible 		= eligibility status, 0 not eligible yet, 1 eligible.
//=					= $dtsday used when MISC_QUEST|128, and signifies that person already voted.
//=		MISC_QUEST		= |128 = eligible and voted.
//=NPC Func. -	arg(0) 			= 0, Cool Event Corp Voting Staff; 1, Kafra Corp Voting Staff
//=		arg(1)			= Kafra only, Cool Corp script does not use. Changes illus for different sprites.
//=					= 0 = 4_f_kafra6 (yellow ponytail, classic outfit, spr 112)
//=					= 1 = 4_f_kafra5 (pink hair, classic outfit, spr 113)
//=					= 2 = 4_f_kafra4 (orange short hair, classic outfit, spr 114)
//=					= 3 = 4_f_kafra3 (brown bangs, classic outfit, spr 115)
//=					= 4 = 4_f_kafra2 (brown ponytale, classic outfit, spr 116)
//=					= 5 = 4_f_kafra1 (blue hair, classic outfit, spr 117)
//=					= 6 = 4_f_agentkafra (purp hair, black outfit, spr 859)
//=					= 7 = 4_f_kafra8 (blue hair, brown outfit, spr 860)
//=					= 8 = 4_f_kafra9 (red hair, brown outfit, spr 861)
//=Temporary - 	.@dtstemp		= Temporary buffer, used in GM vote-fixing
//=		.@dtstemps$		= Temporary buffer, like above, but a string.
//=		$@dtstempg		= Temporary buffer, but global

//===== Comentários Adicional: ===============================
//= 1.0	Initial release [Evera]
//= 1.1	SVN release, removed from major town (found true info after research),
//=	put more arg(2) parameters, fixed a few bugs [Evera]
//= 1.2 Mushed some variables together, optimized a bit [Evera]
//= 1.3 Removed selfconfig [Evera]
//= 1.4 Changed global variables to read from 3 global variables, updated names,
//= 	fixed array bug [Evera]
//= 1.5 Mushed voting varialbes into 1 variable. [Evera]
//= 1.6 Removed Duplicates [Silent]
//= 1.7	Fixed bug of DTS_Admin not running by itself [Evera]
//= 1.8	Changed requirement to lvl 60 [Evera]
//= 1.9 Removed global eligibility option [Evera]
//= 2.0 Fixed zeny bug [Evera]
//= 2.1 Fixed headers with and updated with newer information [Evera]
//= 2.2 Changed dtseligible 2 to MISC_QUEST|128, to clean up variable usage. [Evera]
//=	Also added F_ClearGarbage function to the scripts. [Evera]
//= 2.3	Changed dtseligible to be $dtsday when MISC_QUEST|128
//=	Added $dtsday for vote session counter
//=	Fixed bug that involved having both warpers enabled at the same time. [Evera]
//=	New $dtsday system and bug fix[Evera]
//= 2.3a fixed 2 bugs with comparision [Lupus]
//= 2.3b corrected Glast warp coords [Lupus] 2.3c Bailand -> Bayalan
//= 2.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4a Traduzido. [Puncker]
//============================================================

//Yuno
yuno,153,191,4	script	Assist. da Eleição::CoolEventVotingStaff01	874,{
	callfunc "F_DTS_Warp",0,0;
}
yuno,162,191,6	script	Assist. da Eleição#01	861,{
	callfunc "F_DTS_Warp",1,8;
}
//Lighthalzen
lighthalzen,154,60,6	script	Assist. da Eleição::CoolEventVotingStaff02	874,{
	callfunc "F_DTS_Warp",0,0;
}
//Prontera
prontera,147,125,6	script	Assist. da Eleição::CoolEventVotingStaff03	874,{
	callfunc "F_DTS_Warp",0,0;
}
prontera,164,125,4	script	Assist. da Eleição#02	115,{
	callfunc "F_DTS_Warp",1,3;
}

//Cool Event Staff Headquarters NPCs
//Kudiuu (Maintenance Guy)
lhz_in02,20,274,6	script	Garoto da Manutenção#dts	851,{
	mes "[Kudiuu]";
	mes "Droga...! Este lugar já foi limpo? Há tanto pó aqui, *Cof cof* é quase um risco de morte!";
	close;
}
//Cesuna (Zondaman at desk)
lhz_in02,36,284,1	script	Assist. do Evento#01	874,{
	mes "[Cesuna]";
	mes "Ack! Eu estou totalmente submerso com todo esse trabalho! Mas eu não quero fazer nada disso. É isso! Eu preciso de um tempo.";
	next;
	mes "[Cesuna]";
	mes "*Suspiro...* Eu fico me perguntando se Saera sairia comigo? Isso seria ótimo.";
	close;
}
//Jellarin (Event planner)
lhz_in02,40,279,3	script	Planejador de Evento	833,{
	mes "[Jellarin]";
	mes "Eu não gosto disso. Mas eu não gosto dessa idéia também. O que eu farei para um evento novo?";
	next;
	mes "[Jellarin]";
	mes "Eu preciso de algo maior, algo que realmente tremerá o mundo, algo legal, mas o que? Ei, você tem alguma idéia?";
	close;
}
//Baoto (Cool Event Manager)
lhz_in02,110,283,5	script	Gerente do Evento	853,{
	mes "[Baoto]";
	mes "Hmmm... Os empregados parecem estar se divertindo muito recentemente entre eles. Isto não pressagia nada bem...";
	next;
	mes "[Baoto]";
	mes "Parece que eu terei que estralhar aquele chicote mais freqüentemente e muito mais forte. Ha ha ha ha!";
	close;
}
//Saera (Secretary), contains GM menu
lhz_in02,36,274,3	script	Assist. do Evento#02	831,{
	callfunc "F_ClearGarbage";				//Clear outdated, unused variables
	if($dts == 0){
		set $dts,$dts|(1<<0);				//Sets NPCs to election mode if first time running
		set $dts,$dts|(1<<3);
	}
	if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
		if($dts&(1<<0)) set $dts,$dts&~(1<<0);
		if($dts&(1<<1)) set $dts,$dts&~(1<<1);
		if($dts&(1<<2)) set $dts,$dts&~(1<<2);
		set $dts,$dts|(1<<0);
	}
	if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
		if($dts&(1<<3)) set $dts,$dts&~(1<<3);
		if($dts&(1<<4)) set $dts,$dts&~(1<<4);
		if($dts&(1<<5)) set $dts,$dts&~(1<<5);
		set $dts,$dts|(1<<3);
	}
	mes "[Saera]";
	mes "Bem-vindo à sede temporária da Corporação do Evento Incríveis, como posso lhe ajudar hoje?";
	next;
	if(getgmlevel()>=40){ 						//Unofficial Text, GM-configurable settings for Event (GM level above 40 required)
		mes "[Saera]";
		mes "Por que, eu não fui notificada sobre você, "+strcharinfo(0)+". O que você gostaria de fazer hoje?";
		next;
		menu "Fixar Voto",Lfixvote,
		"Estabelecer teleporte atual",Lsettele,
		"Estabelecer o último vencedor da eleição",Lsetlast,
		"Executar manualmente a checagem de voto",Lmanuvotecheck,
		"Menu normal, por favor",-;
		mes "[Saera]";
		mes "Ok";
		next;
	}
	menu "Sede temporária?",Ltemphead,
	"Votação",Lvoting,"Não, obrigado.",Lnothx;

	Ltemphead:
		mes "[Saera]";
		mes "Nosso edifício principal está sofrendo modificações atualmente, assim nós ficaremos aqui por enquanto.";
		close;

	Lvoting:
		mes "[Saera]";
		mes "Atualmente, a Corporação Kafra e a Corporação de Evento Incríveis estão trabalhando em um programa colaborador que proverá serviços de teletransporte direto para calabouços.";
		next;
		mes "[Saera]";
		mes "Devido questões técnicas, ambas companias não podem prover serviços de teletransporte para o mesmo calabouço. Então, nós estaremos selecionando nossos clientes avaliados para escolher a compania que eles querem.";
		next;
		if((dtseligible == 0 || dtseligible == 3) && baselevel >= 60 && MISC_QUEST&128 == 0) set dtseligible,1;
		//Clear previous var of dtseligible,3 being tried for eligibility already
		if(dtseligible != $dtsday && baselevel >= 60 && MISC_QUEST&128){
			set MISC_QUEST,MISC_QUEST&~128;
			set dtseligible,1;
		}
		switch(dtseligible){
			default:
				mes "[Saera]";
				mes "Somente um número limitado de";
				mes "eleitores será escolhido, então";
				mes "você pode checar sua elegibilidade";
				mes "de votação nas sedes de";
				mes "ambas companias participantes.";
				mes "Obrigado pelo seu apoio~";
				close;
				break;
			case 1:
				mes "[Saera]";
				mes "Parece que você";
				mes "pode votar, então";
				mes "por favor vote";
				mes "em algum representante";
				mes "da Equipe de Votação. Obrigado~";
				close;
				break;
			case 2:
				mes "[Saera]";
				mes "Você pode votar, mas";
				mes "você já votou. Obrigado";
				mes "pela sua participação.";
				close;
				break;
		}

	Lnothx:
		mes "[Saera]";
		mes "Obrigado.";
		mes "Tenha um bom dia.";
		close;
									//Começo das opções de GM
	Lfixvote:							//Fixar Voto
		mes "[Saera]";
		mes "Umm, claro..";
		if($dtsv > 0) mes "Corp. Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp. Kafra está ganhando por "+$dtsv*-1+" votos.";
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		mes "A quem você gostaria de dar votos?";
		next;
		menu "Corp. Kafra",-,"Corp. Eventos Incríveis",Lfixcool;

	Lfixkafra:							//Fixando voto de Kafra
		mes "[Saera]";
		if($dtsv > 0) mes "Corp. Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp. Kafra está ganhando por "+$dtsv*-1+" votos.";	//multiplied by -1 because var is negative
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		mes "Por favor digite a quantia que você deseja dar a Corp Kafra.";
		input .@dtstemp;					//Fixa buffer para voto da Corp Kafra
		next;
		mes "[Saera]";
		mes "Você digitou "+.@dtstemp+" para Kafra";
		if($dtsv > 0) mes "Corp. Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp. Kafra está ganhando por "+$dtsv*-1+" votos.";
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		mes "Você está seguro que quer fazer estas mudanças?";
		next;
		menu "Sim",-,"Não",Lnothx;
		set $dtsv,$dtsv-.@dtstemp;				//Fixa buffer para voto da Corp Kafra
		mes "[Saera]";
		mes "Ok, você fixou os votos";
		mes "da Corporação Kafra.";
		if($dtsv > 0) mes "Corp. Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp. Kafra está ganhando por "+$dtsv*-1+" votos.";
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		close;

	Lfixcool:						//Fixando voto do Evento Legal
		mes "[Saera]";
		mes "Deixe-me achar os papéis...";
		if($dtsv > 0) mes "Corp. Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp. Kafra está ganhando por "+$dtsv*-1+" votos.";
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		mes "Por favor digite o novo voto para Corporação Eventos Incríveis.";
		input .@dtstemp;					//Fixando buffer para voto do Evento Legal
		next;
		mes "[Saera]";
		mes "Você digitou "+.@dtstemp+" para Corporação Eventos Incríveis.";
		if($dtsv > 0) mes "Corp. Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp. Kafra está ganhando por "+$dtsv*-1+" votos.";
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		mes "Você está seguro que quer fazer estas mudanças?";
		next;
		menu "Sim",-,"Não",Lnothx;			//Confirmação
		set $dtsv,@dtsv+.@dtstemp;			//Fixando buffer para voto do Evento Legal
		mes "[Saera]";
		mes "Certo, você fixou os votos da Corporação Eventos Incríveis.";
		if($dtsv > 0) mes "Corp Eventos Incríveis está ganhando por "+$dtsv+" votos.";
		if($dtsv < 0) mes "Corp Kafra está ganhando por "+$dtsv*-1+" votos.";
		if($dtsv == 0) mes "A votação está empatada atualmente.";
		close;

	Lsettele:							//Fixar teleporte de DTS atual
		mes "[Saera]";
		mes "Digite Kafra para habilitar o teletransporte pela Kafra,";
		mes "Incríveis para habilitar o teletransporte pela Eventos Incríveis,";
		mes "Eleição para o modo de eleição,";
		mes "ou Cancelar para cancelar.";
		if($dts&(1<<0)) mes "A eleição está atualmente no modo de eleição.";
		if($dts&(1<<1)) mes "Corporação Eventos Incríveis é atualmente a teletransportadora DTS.";
		if($dts&(1<<2)) mes "Corporação Kafra é atualmente a teletransportadora DTS.";
		next;
		input .@dtstemps$;					//Digitar na escolhas para prevenir erros
		mes "Deixe-me achar os papéis...";
		next;
		if(.@dtstemps$ == "Eleição" || .@dtstemps$ == "eleição"){
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);			//Remove o vencedor prévio
				set $dts,$dts|(1<<0);			//Mudar os estatus da eleição
			}
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<0);
			}
			mes "[Saera]";
			mes "Ajustado para modo de eleição.";
			close;
		}
		if(.@dtstemps$ == "Incríveis" || .@dtstemps$ == "incríveis"){
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<1);
			}
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
			}
			mes "[Saera]";
			mes "Corporação Eventos Incríveis é atualmente a teletransportadora DTS.";
			close;
		}
		if(.@dtstemps$ == "Kafra" || .@dtstemps$ == "kafra"){
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
			}
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
			}
			mes "[Saera]";
			mes "Corporação Kafra é atualmente a teletransportadora DTS.";
			close;
		}
		if(.@dtstemps$ == "Cancelar" || .@dtstemps$ == "cancelar") goto Lnothx;
		mes "[Saera]";
		mes  "Por favor digite um nome correto para a eleição.";
		next;
		goto Lsettele;

	Lsetlast:							//Set last winner
		mes "[Saera]";
		mes "Digite Kafra para eleição registrar para mostrar Kafra,";
		mes "Incríveis para eleição registrar para mostrar Corp Eventos Incríveis,";
		mes "Nenhum para nenhum ganhador nos registros de eleição,";
		mes "ou Cancelar para cancelar.";
		if($dts&(1<<3)) mes "Não há nenhum vencedor prévio.";
		if($dts&(1<<4)) mes "A Corporação Eventos Incríveis foi a última vencedora.";
		if($dts&(1<<5)) mes "A Corporação Kafra foi a última vencedora.";
		next;
		input .@dtstemps$;
		mes "[Saera]";
		mes "Deixe-me achar os papéis...";
		next;
		if(.@dtstemps$ == "Nenhum" || .@dtstemps$ == "nenhum"){
			if($dts&(1<<4)){
				set $dts,$dts&~(1<<4);			//Remove o último vencedor
				set $dts,$dts|(1<<3);			//Fixa o último atual vencedor
			}
			if($dts&(1<<5)){
				set $dts,$dts&~(1<<5);
				set $dts,$dts|(1<<3);
			}
			mes "[Saera]";
			mes "Ajustar registros para não mostrar nenhum ganhador prévio.";
			close;
		}
		if(.@dtstemps$ == "Incríveis" || .@dtstemps$ == "incríveis"){
			if($dts&(1<<3)){
				set $dts,$dts&~(1<<3);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<5)){
				set $dts,$dts&~(1<<5);
				set $dts,$dts|(1<<4);
			}
			mes "[Saera]";
			mes "Corporação Eventos Incríveis é agora o último ganhador.";
			close;
		}
		if(.@dtstemps$ == "Kafra" || .@dtstemps$ == "kafra"){
			if($dts&(1<<3)){
				set $dts,$dts&~(1<<3);
				set $dts,$dts|(1<<5);
			}
			if($dts&(1<<4)){
				set $dts,$dts&~(1<<4);
				set $dts,$dts|(1<<5);
			}
			mes "[Saera]";
			mes "Corporação Kafra é agora o último ganhador.";
			close;
		}
		if(.@dtstemps$ == "Cancelar" || .@dtstemps$ == "cancelar") goto Lnothx;
		mes "[Saera]";
		mes "Por favor, digite um nome correto";
		mes "para o último ganhador.";
		next;
		goto Lsetlast;

	Lmanuvotecheck:
		mes "[Saera]";
		mes "Você tem certeza que quer executar";
		mes "a checagem de votos novamente?";
		next;
		menu "Sim",Lmanuvoteyes,"Não",Lnothx;

	Lmanuvoteyes:
		if($dtsday<4 || $dtsday>=100) set $dtsday,4;
		else set $dtsday,$dtsday+1;
		if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
			if($dts&(1<<0)) set $dts,$dts&~(1<<0);
			if($dts&(1<<1)) set $dts,$dts&~(1<<1);
			if($dts&(1<<2)) set $dts,$dts&~(1<<2);
			set $dts,$dts|(1<<0);
		}
		if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			set $dts,$dts|(1<<3);
		}
		if($dtsv == 0){
			set $@dtstemp,rand(1,2);
			if($@dtstemp == 1) set $dtsv,$dtsv+100;
			else set $dtsv,$dtsv-100;
		}
		if($dtsv > 0){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);			//removes last winner varialbes
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);				//removes winner variable
				set $dts,$dts|(1<<3);				//sets last winner varialbe
				set $dts,$dts|(1<<1);				//sets current winner variable
			}
			if($dts&(1<<1)) set $dts,$dts|(1<<4);
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<5);
			}
			set $dtsv,0;
		}
		else{
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<2)) set $dts,$dts|(1<<5);
			set $dtsv,0;
		}
		mes "[Saera]";
		mes "Checagem de votos executada novamente.";
		if($dts&(1<<0)) mes "A eleição está atualmente no modo eleição.";
		if($dts&(1<<1)) mes "A Corporação Eventos Incríveis é atualmente o teletransportador STC.";
		if($dts&(1<<2)) mes "A Corporação Kafra é atualmente o teletransportador STC.";
		close;
}

//Function for Voting Staff NPC
function	script	F_DTS_Warp	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables
	if($dts == 0){ 						//Sets NPCs to election mode if first time running
		set $dts,$dts|(1<<0);
		set $dts,$dts|(1<<3);
	}
	if((dtseligible == 0 || dtseligible == 3) && baselevel >= 60 && MISC_QUEST&128 == 0) set dtseligible,1;	//Clear previous var of dtseligible,3 being tried for eligibility already
	if(dtseligible != $dtsday && baselevel >= 60 && MISC_QUEST&128){
		set MISC_QUEST,MISC_QUEST&~128;
		set dtseligible,1;
	}
	switch(getarg(0)){
	case 0:
		cutin "zonda_01",2;
		mes "[CEI - Assistente da Eleição]";
		mes "Olá! Não se esqueça de fazer";
		mes "sua voz ser ouvida e ter";
		mes "certeza no seu voto nas eleições";
		mes "entre a Corporação Eventos Incríveis e";
		mes "a Corporação Kafra para o controle do";
		mes "Serviço de Teletransporte aos Calabouços!";
		break;
	case 1:
		switch(getarg(1)){
		case 0: cutin "kafra_06",2; break;			//Finding arguments to find which illust to use
		case 1: cutin "kafra_05",2; break;			//Used Red hair "kafra_09" for black outfit kafra
		case 2: cutin "kafra_04",2; break;			//because I couldn't find a suitable illust
		case 3: cutin "kafra_03",2; break;
		case 4: cutin "kafra_02",2; break;
		case 5: cutin "kafra_01",2; break;
		case 6: cutin "kafra_09",2; break;
		case 7: cutin "kafra_08",2; break;
		case 8: cutin "kafra_09",2; break;
		default: cutin "kafra_09",2; break;
		}
		mes "[Kafra Assistente da Eleição]";
		mes "Saudações, aventureiro. Como você deve estar sabendo, nós estamos promovendo uma eleição para prover os Serviços de Teletransporte aos Calabouços. Como posso ajudá-lo?";
		break;
	}
	next;
	menu "Razão da Eleição",Lelection,"Votar",Lvote,
	"Usar o Serviço de Teletransporte",Lteleport,"Cancelar",Lcancel;

	Lelection: 							//Explicação da razão de eleição
		switch(getarg(0)){
		case 0:
			show "[CEI - Assistente da Eleição]";
			show "A Corporação Eventos Incríveis tem planejado prover um novo Serviço de Teletransporte aos Calabouços para seus clientes, um serviço que não é feito pela Corporação Kafra. Porém...";
			next;
			show "[CEI - Assistente da Eleição]";
			show "A Corporação Kafra que já monopoliza o mercado de teletransporte público, atualmente também teve planos para prover um serviço semelhante.";
			next;
			show "[CEI - Assistente da Eleição]";
			show "Por causa de limitações tecnológicas, só uma compania pode ser escolhida como o provedor deste Serviço de Teletransporte aos Calabouços. Consequentemente, nós deixaremos os clientes decidirem por estas eleições.";
			next;
			show "[CEI - Assistente da Eleição]";
			show "Serão seguradas eleições múltiplas de forma que nossos clientes possam testar os serviços especiais de cada compania para eles. Porém, lembre-se de que você deve ser qualificado para votar.";
			next;
			show "[CEI - Assistente da Eleição]";
			show "Para elegibilidade de eleitor detalhada, por favor visite nossa sede na cidade de Lighthalzen localizada na República de Schwarzwald. Obrigada por seu tempo.";
			break;
		case 1:
			show "[Kafra Assistente da Eleição]";
			show "A Corporação Eventos Incríveis e a Corporação Kafra que ambos têm planejado prover um Serviço de Teletransporte aos Calabouços.";
			next;
			show "[Kafra Assistente da Eleição]";
			show "Mas devido a limitações tecnológicas, só uma compania pode servir de cada vez como provedor para este Serviço de Teletransporte aos Calabouço. Então, ambas as corporações concordaram em organizar uma eleição especial.";
			next;
			show "[Kafra Assistente da Eleição]";
			show "Cada corporação tem suas próprias políticas e garantias com respeito ao Serviço de Teletransporte aos Calabouços, e nesta eleição, os clientes decidirão e escolherão o que é melhor para eles.";
			next;
			show "[Kafra Assistente da Eleição]";
			show "Para agora, o Serviço de Teletransporte aos Calabouços será provido em uma série de períodos de testes. Deste modo, os clientes podem ver os benefícios de ambas as companias antes de fazer a decisão final.";
			next;
			show "[Kafra Assistente da Eleição]";
			show "Se você está qualificado, por favor vote em cada eleição para decidir qual compania proverá o Serviço de Teletransporte aos Calabouços do próximo período de testes. Obrigada por seu apoio.";
			break;
		}
		goto Lend;

	Lvote: //If you clicked you wanted to vote
		if(dtseligible == 1) goto Leligible; 			//Varredura de check se é elegível

	Lnoteligible: 							//Text displayed if wanted to vote, but not eligible/voted
		switch(getarg(0)){
		case 0:
			mes "[CEI - Assistente da Eleição]";
			mes "Eu sinto muito, mas você não está qualificado para votar neste momento. Por favor visite nossa sede em Lighthalzen para informação relacionada a adquirir direitos de votação. Obrigado por seu tempo.";
			break;
		case 1:
			mes "[Kafra Assistente da Eleição]";
			mes "Oh, eu sinto muito, mas você não está qualificado para votar atualmente. Para informação de qualificação de voto, por favor visite nossa sede em Al De Baran. Obrigada e tenha um dia agradável.";
			break;
		}
		goto Lend;

	Leligible: 							//Text displayed if eligible to vote
		if(MISC_QUEST&128 && $dtsday == dtseligible) goto Lnoteligible; //Var check if voted
		switch(getarg(0)){
		case 0:
			mes "[CEI - Assistente da Eleição]"; 		//Texto não oficial
			mes "Conferindo suas credenciais, você pode votar na eleições do Serviço de Teletransporte aos Calabouços. Você gostaria de votar para...";
			break;
		case 1:
			mes "[Kafra Assistente da Eleição]";
			mes "Me parece que você é qualificado a votar na eleição do Serviço de Teletransporte aos Calabouços. Você gostaria de votar para...";		//End Unofficial text
			break;
		}
		next;
		menu "Corp. Eventos Incríveis",Lvotecool,"Corporação Kafra",Lvotekafra;
	
	Lvotecool:
		set $dtsv,$dtsv+1;				//Adds 1 to cool count
		set MISC_QUEST,MISC_QUEST|128;				//Sets var so that you can't vote over and over
		set dtseligible,$dtsday;
		switch(getarg(0)){
		case 0:
			mes "[CEI - Assistente da Eleição]";
			mes "Obrigado por votar em nós.";
			mes "Seu voto foi contado,";
			mes "e nós apreciamos seu voto.";
			mes "Obrigado e tenha um bom dia.";
			break;
		case 1:
			mes "[Kafra Assistente da Eleição]";
			mes "Nós agradecemos pelo seu voto.";
			mes "Sua opinião significa muito";
			mes "e foi contada para a Corporação";
			mes "Eventos Incríveis.";
			break;
		}
		goto Lend;

	Lvotekafra:
		setd $dtsv,$dtsv-1; 				//Adds 1 to kafra count
		set MISC_QUEST,MISC_QUEST|128; 			//Sets var to prevent cheating
		set dtseligible,$dtsday;
		switch(getarg(0)){
		case 0:
			mes "[CEI - Assistente da Eleição]";		//Unofficial Text
			mes "Obrigado pela sua opinião.";
			mes "Seu voto para a Corporação Kafra";
			mes "foi contado. Obrigado";
			mes "e tenha um bom dia.";
			break;
		case 1:
			mes "[Kafra Assistente da Eleição]";
			mes "Seu voto para nós foi";
			mes "contado. Nós agradecemos";
			mes "muito pelo seu voto, e";
			mes "esperamos que satisfaçamos suas necessidades";
			mes "em suas aventuras e";
			mes "padrões de excelência.";
			break;
		}
		goto Lend;						//End Unofficial text

	Lteleport: 							//Selected Teleport
		switch(getarg(0)){
		case 0:
			if($dts&(1<<0)){
				mes "[CEI - Assistente da Eleição]";
				mes "Lamento, mas o serviço";
				mes "de Teletransporte para Calabouços fica";
				mes "indisponível durante as eleições";
				mes "e será reativado após os";
				mes "resultados das eleições serem anunciados.";
				mes "Obrigado e tenha um bom dia.";
				goto Lend;
			}
			if($dts&(1<<1)) goto Lteleenabled;
			mes "[CEI - Assistente da Eleição]";		//Text if Kafra won DTS elect.
			mes "Lamento, mas atualmente a Corporação";
			mes "Eventos Incríveis não oferece";
			mes "o serviço de Teletransporte para Calabouços";
			mes "devido aos resultados da última";
			mes "eleição. Por favor, vote em nós";
			mes "da próxima vez, certo? Bom dia~";
			break;
		case 1:
			if($dts&(1<<0)){
				mes "[Kafra Assistente da Eleição]";
				mes "Desculpe, mas as eleições para";
				mes "escolher o Sistema de Teletransporte para Calabouços";
				mes "estão acontecendo agora mesmo.";
				mes "Nós estamos incapazes de prestar serviço";
				mes "até que os votos sejam anunciados.";
				goto Lend;
			}
			if($dts&(1<<2)) goto Lteleenabled;
			mes "[Kafra Assistente da Eleição]";
			mes "Nós lamentamos, mas a atualmente";
			mes "a Corporação Kafra não oferece o";
			mes "Sistema de Teletransporte para Calabouços";
			mes "devido aos resultados da última eleição.";
			mes "Por favor, vote na Corporação Kafra";
			mes "da próxima vez~";
			break;
		}
		goto Lend;

	Lteleenabled: 							//Shows DTS tele selections
	cleararray @dtswarpmap$[0],"",getarraysize(@dtswarpmap$);
	cleararray @dtswarp$[0],"",getarraysize(@dtswarp$);
	switch(getarg(0)){
	case 0:
		mes "[CEI - Assistente da Eleição]";
		mes "Por favor, lembre-se que nós";
		mes "não podemos aceitar Vales-Teletransporte";
		mes "ou dar Pontos de Reserva Especiais";
		mes "por esse serviço. Agora, por favor";
		mes "escolha seu destino.";
		setarray @dtswarpmap$[0],"Calabouço de Byalan, Nível 4","Torre do Relógio, Base 3";
		setarray @dtswarp$[0],@dtswarpmap$[0]+" -> 4,000z",
		@dtswarpmap$[1]+" -> 4,000z","Cancelar";
		if($dts&(1<<4)){ 
			set @dtswarpmap$[2],"Entrada de Glastheim";
			setarray @dtswarp$[2],@dtswarpmap$[2]+" -> 4,000z","Cancelar";
		}
		break;
	case 1:
		mes "[Kafra Assistente da Eleição]";
		mes "Obrigada por escolher o";
		mes "Serviço de Teletransporte para Calabouços.";
		mes "Por favor, tenha em mente que os";
		mes "Vales-Teletransporte e os";
		mes "Pontos de Reserva Especiais não se";
		mes "aplicam nesse serviço especial.";
		setarray @dtswarpmap$[0],"Fábrica de Brinquedos, Nível 2","Torre do Relógio, Nível 3";
		setarray @dtswarp$[0],@dtswarpmap$[0]+" -> 4,000z",
		@dtswarpmap$[1]+" -> 4,000z","Cancelar";
		if($dts&(1<<5)){ 
			set @dtswarpmap$[2],"Calabouço de Magma, Nível 2";
			setarray @dtswarp$[2],@dtswarpmap$[2]+" -> 4,000z","Cancelar";
		}
		break;
	}
	next;
	switch(select(@dtswarp$[0],@dtswarp$[1],@dtswarp$[2],@dtswarp$[3])){
		case 1:
			set @num, 0;
			break;
		case 2:
			set @num, 1;
			break;
		case 3:
			set @num, 2;
			break;
		case 4:
			set @num, 3;
			break;
	}
	Lwarp:
		if (@dtswarp$[@num] == "Cancelar") goto Lcancel;
		if (Zeny<4000) goto Lnomoney;
		set Zeny, Zeny-4000;
		if (@dtswarpmap$[@num] == "Fábrica de Brinquedos, Nível 2") warp "xmas_dun02",130,123;
 //Maps to warp to
		if (@dtswarpmap$[@num] == "Torre do Relógio, Nível 3") warp "alde_dun03",265,22;

		if (@dtswarpmap$[@num] == "Calabouço de Magma, Nível 2") warp "mag_dun02",47,40;

		if (@dtswarpmap$[@num] == "Calabouço de Byalan, Nível 4") warp "iz_dun03",32,63;

		if (@dtswarpmap$[@num] == "Torre do Relógio, Base 3") warp "alde_dun03",277,178;

		if (@dtswarpmap$[@num] == "Entrada de Glastheim") warp "glast_01",370,304;

		cutin "", 255;
		end;
	Lnomoney:
		switch(getarg(0)){
		case 0:
			mes "[CEI - Assistente da Eleição]"; 		//Unofficial Text
			mes "Umm, aparentemente você não tem";
			mes "fundos adequados para o local";
			mes "selecionado. Por favor, verifique";
			mes "se você possui a quantia correta";
			mes "de dinheiro, e tente novamente mais tarde.";
			break;
		case 1:
			mes "[Kafra Assistente da Eleição]";
			mes "Parece que você não tem";
			mes "zeny suficiente para o local selecionado.";
			mes "Por favor, verifique seus fundos e";
			mes "tente novamente.";
			break;
		}
		goto Lend;						//End unofficial text

	Lcancel: 							//Selected Cancel on first menu
		switch(getarg(0)){
		case 0:
			mes "[CEI - Assistente da Eleição]";	
			mes "A Corporação Eventos Incríveis está sempre";
			mes "trabalhando para ter certeza de que";
			mes "nossos clientes não estão apenas";
			mes "satisfeitos, mas também de que";
			mes "excedemos nossas qualidades mais elevadas.";
			mes "Obrigado e tenha um bom dia.";
			break;
		case 1:
			mes "[Kafra Assistente da Eleição]";
			mes "Nós, aqui da Corporação Kafra,";
			mes "estamos sempre nos esforçando para dar";
			mes "a você os melhores serviços. Nós esperamos";
			mes "que satisfaçamos suas necessidades de aventura";
			mes "e os padrões de excelência.";
			break;
		}
		goto Lend;
	Lend:
		close2;
		cutin "", 255;
		end;

}

//Elections administration NPC (hidden)
-	script	DTS_Admin	-1,{
	OnSun0100:					//Works only at 1am on sunday
		if($dtsday<4 || $dtsday>=100) set $dtsday,4;
		else set $dtsday,$dtsday+1;
		if($dts == 0){
			set $dts,$dts|(1<<0);
			set $dts,$dts|(1<<3);
		}
		if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
			if($dts&(1<<0)) set $dts,$dts&~(1<<0);
			if($dts&(1<<1)) set $dts,$dts&~(1<<1);
			if($dts&(1<<2)) set $dts,$dts&~(1<<2);
			set $dts,$dts|(1<<0);
		}
		if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			set $dts,$dts|(1<<3);
		}
		if($dtsv == 0){				//If tied, gives random side 100 votes
			set $@dtstemp,rand(1,2);
			if($@dtstemp == 1) set $dtsv,$dtsv+100;
			else set $dtsv,$dtsv-100;
		}
		if($dtsv > 0){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)) set $dts,$dts|(1<<4);
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<5);
			}
			set $dtsv,0;
		}
		else{
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<2)) set $dts,$dts|(1<<5);
			set $dtsv,0;
		}
		end;
}
