// ==============================================================================
// Batalhas Campais - Flavius 2
//===== Changelog: ==============================================================
//= Removido show [Yum]
//===============================================================================

// Registration NPC's
// *********************************************************************

bat_room,57,97,4	script	Registro::Fl2R_Guillaume	418,{
	end;
OnInit:
	waitingroom "Estação de Batalha 10 Jogadores",10,"Flavius_BG2::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id1, waitingroom2bg("bat_b02",390,10,"Flavius_BG2::OnGuillaumeQuit","");
	end;
}

bat_room,57,74,0	script	Registro::Fl2R_Croix	414,{
	end;
OnInit:
	waitingroom "Estação de Batalha 10 Jogadores",10,"Flavius_BG2::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id2, waitingroom2bg("bat_b02",10,290,"Flavius_BG2::OnCroixQuit","");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG2	-1,{
	end;

OnInit:
	disablenpc "Negociante Guillaume#fl2";
	disablenpc "Negociante de Croix#fl2";
	disablenpc "Terapeuta#fl21";
	disablenpc "Terapeuta#fl22";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG2 == 0 )
		donpcevent "Flavius_BG2::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG2 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl2R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl2R_Croix");
	
	if( .@Guillaume < 10 || .@Croix < 10 )
	{
		mapannounce "bat_room","Batalha Campal -- Flavius [80-99] G: " + .@Guillaume + "/10, C: " + .@Croix + "/10",1,0x808000;
		end;
	}

	// BG Variables
	set $@FlaviusBG2, 1;
	set $@FlaviusBG2_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl2_respawn::OnBGStart";
	donpcevent "#cfl2_respawn::OnBGStart";
	enablenpc "Terapeuta#fl21";
	enablenpc "Terapeuta#fl22";
	disablenpc "Negociante Guillaume#fl2";
	disablenpc "Negociante de Croix#fl2";
	// Build and Warp Teams
	donpcevent "Fl2R_Guillaume::OnEnterBG";
	donpcevent "Fl2R_Croix::OnEnterBG";
	announce "Batalha Campal -- Flavius [80-99] começou!",0,0x808000;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	areapercentheal "bat_b02",382,2,397,17,100,100;
	areapercentheal "bat_b02",2,282,17,297,100,100;
	bg_warp $@FlaviusBG2_id1,"bat_b02",311,224;
	bg_warp $@FlaviusBG2_id2,"bat_b02",87,75;

	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG2_id1,"bat_b02",328,150,"Cristal de Guillaume",1914,"Flavius_BG2::OnGuillaumeBreak");
	setwall "bat_b02",327,149,2,6,0,"gui_wall2-1";
	setwall "bat_b02",329,149,2,0,0,"gui_wall2-2";
	setwall "bat_b02",329,151,2,2,0,"gui_wall2-3";
	setwall "bat_b02",327,151,2,4,0,"gui_wall2-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG2_id2,"bat_b02",62,150,"Cristal de Croix",1915,"Flavius_BG2::OnCroixBreak");
	setwall "bat_b02",61,149,2,6,0,"cro_wall2-1";
	setwall "bat_b02",63,149,2,0,0,"cro_wall2-2";
	setwall "bat_b02",63,151,2,2,0,"cro_wall2-3";
	setwall "bat_b02",61,151,2,4,0,"cro_wall2-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,160,"Guardião de Guillaume",1949,"Flavius_BG2::OnGuiGuardian";
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,140,"Guardião de Guillaume",1950,"Flavius_BG2::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG2_id2,"bat_b02",62,160,"Guardião de Croix",1949,"Flavius_BG2::OnCroGuardian";
	bg_monster $@FlaviusBG2_id2,"bat_b02",61,140,"Guardião de Croix",1950,"Flavius_BG2::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b02","A Batalha de Flavius ["+.Match+"] Round começou!!",1,0x808000;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b02","Flavius_BG2::OnGuiGuardian";
	killmonster "bat_b02","Flavius_BG2::OnCroGuardian";
	delwall "gui_wall2-1";
	delwall "gui_wall2-2";
	delwall "gui_wall2-3";
	delwall "gui_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnGuillaumeBreak";
	delwall "cro_wall2-1";
	delwall "cro_wall2-2";
	delwall "cro_wall2-3";
	delwall "cro_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall2-1";
		delwall "gui_wall2-2";
		delwall "gui_wall2-3";
		delwall "gui_wall2-4";
		mapannounce "bat_b02","O Cristal de Guillaume está vulnerável a ataques!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall2-1";
		delwall "cro_wall2-2";
		delwall "cro_wall2-3";
		delwall "cro_wall2-4";
		mapannounce "bat_b02","O Cristal de Croix está vulnerável a ataques!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","O Cristal de Guillaume foi destruído!",1,0x0000FF;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","O exército de Croix venceu a Batalha de Flavius!",1,0xFF0000;
		set $@FlaviusBG2_Victory, 2;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","O Cristal de Croix foi destruído!",1,0xFF0000;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","O exército de Guillaume venceu a Batalha de Flavius!",1,0x0000FF;
		set $@FlaviusBG2_Victory, 1;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b02","A Batalha de Flavius terminará em 5 minutos",1,0x808000;
	end;
OnTimer2640000:
	mapannounce "bat_b02","A Batalha de Flavius terminará em 1 minuto",1,0x808000;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b02","O exército de Croix venceu a Batalha de Flavius!",1,0xFF0000;
		set $@FlaviusBG2_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b02","O exército de Guillaume venceu a Batalha de Flavius!",1,0x0000FF;
		set $@FlaviusBG2_Victory, 1;
	}
	else
	{
		mapannounce "bat_b02","A Batalha de Flavius terminou. O tempo acabou, temos um Empate",1,0x808000;
		set $@FlaviusBG2_Victory, 3;
	}

OnMatchEnd:
	set $@FlaviusBG2, 2;
	stopnpctimer;
	donpcevent "#gfl2_respawn::OnBGStop";
	donpcevent "#cfl2_respawn::OnBGStop";
	disablenpc "Terapeuta#fl21";
	disablenpc "Terapeuta#fl22";
	enablenpc "Negociante Guillaume#fl2";
	enablenpc "Negociante de Croix#fl2";
	sleep 2000;
	bg_warp $@FlaviusBG2_id1,"bat_b02",390,10;
	bg_warp $@FlaviusBG2_id2,"bat_b02",10,290;
	sleep 3000;
	mapannounce "bat_b02","A Batalha de Flavius fechará em 1 minuto!",1,0x808000;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02","A Batalha de Flavius fechará em 30 segundos!",1,0x808000;
	end;
OnTimer50000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02","A Batalha de Flavius fechará em 10 segundos!",1,0x808000;
	end;

OnTimer60000:
	if( $@FlaviusBG2 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG2_Victory, 0;
	// NPC's
	disablenpc "Negociante Guillaume#fl2";
	disablenpc "Negociante de Croix#fl2";
	disablenpc "Terapeuta#fl21";
	disablenpc "Terapeuta#fl22";

	if( $@FlaviusBG2_id1 ) { bg_destroy $@FlaviusBG2_id1; set $@FlaviusBG2_id1, 0; }
	if( $@FlaviusBG2_id2 ) { bg_destroy $@FlaviusBG2_id2; set $@FlaviusBG2_id2, 0; }
	sleep 1000;
	mapwarp "bat_b02","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_b02",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b02",0,0;
	set $@FlaviusBG2, 0;
	donpcevent "Flavius_BG2::OnReadyCheck";
	end;
}

// Battleground rewards
// *********************************************************************

bat_b02,390,13,5	script	Negociante Guillaume#fl2	419,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == Bat_Team )
		{ // Victory
			mes "[Swandery]";
			mes "Abençoado seja Guillaume!!";
			mes "Vamos desfrutar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", isto é uma recompensa em sinal de nossa gratidão.";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "Você perdeu, mas foi dedicado nesta batalha.";
			mes "Isto é uma recompensa pela sua grande dedicação pelo Guillaume Marollo!";
			mes "Tenha esta derrota como uma lição, e mais tarde você definitivamente aprenderá.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_b02,10,293,5	script	Negociante de Croix#fl2	415,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == Bat_Team )
		{ // Victory
			mes "[Swandery]";
			mes "Abençoado seja Croix!!";
			mes "Vamos desfrutar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", isto é uma recompensa em sinal de nossa gratidão.";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Não fique triste.";
			mes "Nos podemos não ter ganho, mas fizemos nosso melhor.";
			mes "Este é um presente Real de Croix, e por favor não se esqueça desta batalha. Nós podemos vencer a próxima.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b02,390,13,5	script	Terapeuta#fl22	95,{
	mes "[Terapeuta]";
	mes "Apenas feche seus olhos, e respire fundo.";
	mes "Você está livre do sofrimento.";
	specialeffect2 312;
	close;
}

bat_b02,10,293,5	script	Terapeuta#fl21	95,{
	mes "[Terapeuta]";
	mes "Apenas feche seus olhos, e respire fundo.";
	mes "Você está livre do sofrimento.";
	specialeffect2 312;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_b02,390,10,0	script	#gfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",382,2,397,17,100,100;
	areawarp "bat_b02",382,2,397,17,"bat_b02",311,224;
	initnpctimer;
	end;
}

bat_b02,10,290,0	script	#cfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",2,282,17,297,100,100;
	areawarp "bat_b02",2,282,17,297,"bat_b02",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b02,304,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat32	973
bat_b02,319,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat33	973
bat_b02,304,218,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat34	973
bat_b02,319,218,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat35	973
bat_b02,304,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat36	973
bat_b02,304,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat37	973
bat_b02,335,142,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat38	973
bat_b02,335,157,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat39	973
bat_b02,390,16,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat40	973
bat_b02,292,163,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat41	973
bat_b02,292,136,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat42	973
bat_b02,241,185,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat43	973
bat_b02,247,179,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat44	973

bat_b02,96,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat30	974
bat_b02,96,68,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat31	974
bat_b02,79,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat32	974
bat_b02,79,68,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat33	974
bat_b02,96,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat34	974
bat_b02,96,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat35	974
bat_b02,59,164,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat36	974
bat_b02,59,137,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat37	974
bat_b02,10,296,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat38	974
bat_b02,110,162,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat39	974
bat_b02,110,137,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat40	974
bat_b02,152,120,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat41	974
bat_b02,158,114,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat42	974

// MapFlags
// *********************************************************************

bat_b02	mapflag	battleground	2
bat_b02	mapflag	nomemo
bat_b02	mapflag	nosave	SavePoint
bat_b02	mapflag	noteleport
bat_b02	mapflag	nowarp
bat_b02	mapflag	nowarpto
bat_b02	mapflag	noreturn
bat_b02	mapflag	nobranch
bat_b02	mapflag	nopenalty