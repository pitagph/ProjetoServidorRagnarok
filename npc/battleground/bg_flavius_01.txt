// ==============================================================================
// Batalhas Campais - Flavius 1
//===== Changelog: ==============================================================
//= Removido show [Yum]
//===============================================================================

// Registration NPC's
// *********************************************************************

bat_room,253,97,4	script	Registro::Fl1R_Guillaume	418,{
	end;
OnInit:
	waitingroom "Estação de Batalha 10 Jogadores",10,"Flavius_BG1::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",390,10,"Flavius_BG1::OnGuillaumeQuit","");
	end;
}

bat_room,253,74,0	script	Registro::Fl1R_Croix	414,{
	end;
OnInit:
	waitingroom "Estação de Batalha 10 Jogadores",10,"Flavius_BG1::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",10,290,"Flavius_BG1::OnCroixQuit","");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG1	-1,{
	end;

OnInit:
	disablenpc "Negociante Guillaume#fl1";
	disablenpc "Negociante de Croix#fl1";
	disablenpc "Terapeuta#fl11";
	disablenpc "Terapeuta#fl12";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG1 == 0 )
		donpcevent "Flavius_BG1::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl1R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl1R_Croix");
	
	if( .@Guillaume < 10 || .@Croix < 10 )
	{
		mapannounce "bat_room","Batalha Campal -- Flavius [80-99] G: " + .@Guillaume + "/10, C: " + .@Croix + "/10",1,0x006400;
		end;
	}

	// BG Variables
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl1_respawn::OnBGStart";
	donpcevent "#cfl1_respawn::OnBGStart";
	enablenpc "Terapeuta#fl11";
	enablenpc "Terapeuta#fl12";
	disablenpc "Negociante Guillaume#fl1";
	disablenpc "Negociante de Croix#fl1";
	// Build and Warp Teams
	donpcevent "Fl1R_Guillaume::OnEnterBG";
	donpcevent "Fl1R_Croix::OnEnterBG";
	announce "Batalha Campal -- Flavius [80-99] começou!",0,0x006400;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	areapercentheal "bat_b01",382,2,397,17,100,100;
	areapercentheal "bat_b01",2,282,17,297,100,100;
	bg_warp $@FlaviusBG1_id1,"bat_b01",311,224;
	bg_warp $@FlaviusBG1_id2,"bat_b01",87,75;

	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG1_id1,"bat_b01",328,150,"Cristal de Guillaume",1914,"Flavius_BG1::OnGuillaumeBreak");
	setwall "bat_b01",327,149,2,6,0,"gui_wall1-1";
	setwall "bat_b01",329,149,2,0,0,"gui_wall1-2";
	setwall "bat_b01",329,151,2,2,0,"gui_wall1-3";
	setwall "bat_b01",327,151,2,4,0,"gui_wall1-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG1_id2,"bat_b01",62,150,"Cristal de Croix",1915,"Flavius_BG1::OnCroixBreak");
	setwall "bat_b01",61,149,2,6,0,"cro_wall1-1";
	setwall "bat_b01",63,149,2,0,0,"cro_wall1-2";
	setwall "bat_b01",63,151,2,2,0,"cro_wall1-3";
	setwall "bat_b01",61,151,2,4,0,"cro_wall1-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,160,"Guardião de Guillaume",1949,"Flavius_BG1::OnGuiGuardian";
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,140,"Guardião de Guillaume",1950,"Flavius_BG1::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG1_id2,"bat_b01",62,160,"Guardião de Croix",1949,"Flavius_BG1::OnCroGuardian";
	bg_monster $@FlaviusBG1_id2,"bat_b01",61,140,"Guardião de Croix",1950,"Flavius_BG1::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b01","A Batalha de Flavius ["+.Match+"] Round começou!!",1,0x006400;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b01","Flavius_BG1::OnGuiGuardian";
	killmonster "bat_b01","Flavius_BG1::OnCroGuardian";
	delwall "gui_wall1-1";
	delwall "gui_wall1-2";
	delwall "gui_wall1-3";
	delwall "gui_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnGuillaumeBreak";
	delwall "cro_wall1-1";
	delwall "cro_wall1-2";
	delwall "cro_wall1-3";
	delwall "cro_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall1-1";
		delwall "gui_wall1-2";
		delwall "gui_wall1-3";
		delwall "gui_wall1-4";
		mapannounce "bat_b01","O Cristal de Guillaume está vulnerável a ataques!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall1-1";
		delwall "cro_wall1-2";
		delwall "cro_wall1-3";
		delwall "cro_wall1-4";
		mapannounce "bat_b01","O Cristal de Croix está vulnerável a ataques!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","O Cristal de Guillaume foi destruído!",1,0x0000FF;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","O exército de Croix venceu a Batalha de Flavius!",1,0xFF0000;
		set $@FlaviusBG1_Victory, 2;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","O Cristal de Croix foi destruído!",1,0xFF0000;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","O exército de Guillaume venceu a Batalha de Flavius!",1,0x0000FF;
		set $@FlaviusBG1_Victory, 1;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b01","A Batalha de Flavius terminará em 5 minutos",1,0x006400;
	end;
OnTimer2640000:
	mapannounce "bat_b01","A Batalha de Flavius terminará em 1 minuto",1,0x006400;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b01","O exército de Croix venceu a Batalha de Flavius!",1,0xFF0000;
		set $@FlaviusBG1_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b01","O exército de Guillaume venceu a Batalha de Flavius!",1,0x0000FF;
		set $@FlaviusBG1_Victory, 1;
	}
	else
	{
		mapannounce "bat_b01","A Batalha de Flavius terminou. O tempo acabou, temos um Empate",1,0x006400;
		set $@FlaviusBG1_Victory, 3;
	}

OnMatchEnd:
	set $@FlaviusBG1, 2;
	stopnpctimer;
	donpcevent "#gfl1_respawn::OnBGStop";
	donpcevent "#cfl1_respawn::OnBGStop";
	disablenpc "Terapeuta#fl11";
	disablenpc "Terapeuta#fl12";
	enablenpc "Negociante Guillaume#fl1";
	enablenpc "Negociante de Croix#fl1";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",390,10;
	bg_warp $@FlaviusBG1_id2,"bat_b01",10,290;
	sleep 3000;
	mapannounce "bat_b01","A Batalha de Flavius fechará em 1 minuto!",1,0x006400;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","A Batalha de Flavius fechará em 30 segundos!",1,0x006400;
	end;
OnTimer50000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","A Batalha de Flavius fechará em 10 segundos!",1,0x006400;
	end;

OnTimer60000:
	if( $@FlaviusBG1 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG1_Victory, 0;
	// NPC's
	disablenpc "Negociante Guillaume#fl1";
	disablenpc "Negociante de Croix#fl1";
	disablenpc "Terapeuta#fl11";
	disablenpc "Terapeuta#fl12";

	if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; set $@FlaviusBG1_id1, 0; }
	if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; set $@FlaviusBG1_id2, 0; }
	sleep 1000;
	mapwarp "bat_b01","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b01",0,0;
	set $@FlaviusBG1, 0;
	donpcevent "Flavius_BG1::OnReadyCheck";
	end;
}

// Battleground rewards
// *********************************************************************

bat_b01,390,13,5	script	Negociante Guillaume#fl1	419,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // Victory
			mes "[Swandery]";
			mes "Abençoado seja Guillaume!!";
			mes "Vamos desfrutar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", isto é uma recompensa em sinal de nossa gratidão.";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "Você perdeu, mas foi dedicado nesta batalha.";
			mes "Isto é uma recompensa pela sua grande dedicação pelo Guillaume Marollo!";
			mes "Tenha esta derrota como uma lição, e mais tarde você definitivamente aprenderá.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_b01,10,293,5	script	Negociante de Croix#fl1	415,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // Victory
			mes "[Swandery]";
			mes "Abençoado seja Croix!!";
			mes "Vamos desfrutar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", isto é uma recompensa em sinal de nossa gratidão.";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Não fique triste.";
			mes "Nos podemos não ter ganho, mas fizemos nosso melhor.";
			mes "Este é um presente Real de Croix, e por favor não se esqueça desta batalha. Nós podemos vencer a próxima.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b01,390,13,5	script	Terapeuta#fl12	95,{
	mes "[Terapeuta]";
	mes "Apenas feche seus olhos, e respire fundo.";
	mes "Você está livre do sofrimento.";
	specialeffect2 312;
	close;
}

bat_b01,10,293,5	script	Terapeuta#fl11	95,{
	mes "[Terapeuta]";
	mes "Apenas feche seus olhos, e respire fundo.";
	mes "Você está livre do sofrimento.";
	specialeffect2 312;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_b01,390,10,0	script	#gfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",382,2,397,17,100,100;
	areawarp "bat_b01",382,2,397,17,"bat_b01",311,224;
	initnpctimer;
	end;
}

bat_b01,10,290,0	script	#cfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",2,282,17,297,100,100;
	areawarp "bat_b01",2,282,17,297,"bat_b01",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b01,304,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat1	973
bat_b01,319,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat2	973
bat_b01,304,218,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat3	973
bat_b01,319,218,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat4	973
bat_b01,304,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat5	973
bat_b01,304,231,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat6	973
bat_b01,335,142,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat7	973
bat_b01,335,157,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat8	973
bat_b01,390,16,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat9	973
bat_b01,292,163,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat19	973
bat_b01,292,136,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat20	973
bat_b01,241,185,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat21	973
bat_b01,247,179,1	duplicate(Bandeira Guillaume#bat)	Bandeira Guillaume#bat22	973

bat_b01,96,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat1	974
bat_b01,96,68,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat2	974
bat_b01,79,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat3	974
bat_b01,79,68,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat4	974
bat_b01,96,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat5	974
bat_b01,96,81,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat6	974
bat_b01,59,164,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat7	974
bat_b01,59,137,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat8	974
bat_b01,10,296,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat9	974
bat_b01,110,162,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat18	974
bat_b01,110,137,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat19	974
bat_b01,152,120,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat20	974
bat_b01,158,114,1	duplicate(Bandeira Croix#bat)	Bandeira Croix#bat21	974

// MapFlags
// *********************************************************************

bat_b01	mapflag	battleground	2
bat_b01	mapflag	nomemo
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	noteleport
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty