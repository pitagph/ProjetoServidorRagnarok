//===== Cronus Script ========================================
//= Cidade de Alberta
//===== Por: =================================================
//= DZeroX
//===== Versão Atual: ========================================
//= 1.5
//===== Compatível Com: ======================================
//= Cronus SVN
//===== Descrição: ===========================================
//= Npcs da Cidade de Alberta
//===== Comentários Adicionais: ==============================
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.4a Traduzido script [Lich]
//= 1.4b Revisão geral no Script [Asus]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//============================================================

alberta,97,51,0	script	Fabian	84,{
	mes "[Fabian]";
	mes "Cara... Quando você viajar por todo o mundo, você vai escutar algumas coisas estranhas.";
	next;
	mes "[Fabian]";
	mes "Uma vez, eu escutei que existem cartas que contêm o poder de monstros. Se alguém conseguir por as mãos numa carta, eles serão capazes de usar o poder daquele monstro.";
	next;
	mes "[Fabian]";
	mes "Eu acho que é algum tipo de mania ou truque que eles usam para fazer você coletar todas as cartas ou o que for. Quero dizer, como é possivel que uma carta contenha o poder de um monstro?!";
	next;
	mes "[Fabian]";
	mes "Sério...";
	close;
}

alberta,53,39,0	script	Steiner	100,{
	mes "[Steiner]";
	mes "Oh...! Bem-vindo a Alberta, jovem aventureiro!";
	next;
	mes "[Steiner]";
	mes "Perdoe-me se eu pareço distraído. Eu estou pensando, tentando fazer um plano. Veja, eu ouvi que existe uma loja em Geffen que vende amaduras resistentes à magia.";
	next;
	mes "[Steiner]";
	mes "Se eu comprar muitos à varejo, e revender eles aqui por um preço mais alto...";
	close;
}

alberta,20,183,0	script	Chad	49,{
	mes "[Chad]";
	mes "Pessoas dizem que a arma legendária Gungnir nunca erra seu alvo. Eu me pergunto se isso é verdade...";
	next;
	mes "[Chad]";
	mes "Pessoas também dizem que bebês são montados por cegonhas antes de serem entregados, garotas desejam caras que agem como idiotas, e Papai Noel existe! Mas apenas em Lutie.";
	next;
	mes "[Chad]";
	mes "Eu me pergunto... Se alguma coisa disso é verdade...";
	close;
}

alberta,131,139,2	script	Velho Bêbado	54,{
	mes "[Deagle]";
	mes "*^CCCCCCHiccup^000000*";
	mes "Oq-O quê você está";
	mes "olhando? Se manda!!";
	next;	
	switch(select("Falar nada.","Deixar ele em paz.")) {
		case 1:
			mes "[Deagle]";
			mes "Hahahahaha *^CCCCCHicks^000000*... Você tem cara-de-pau. Eu posso parecer inútil agora, mas eu costumava ser um marinheiro a bordo do 'Going Mary.'";
			next;
			switch(select("Nunca ouvi falar.","Sério? Você está brincando!")) {
				case 1:
					mes "[Deagle]";
					mes "Nunca ouviu falar?! Todo mundo conhece o notório navio pirata 'Going Mary!' *^CCCCCCHicks~^000000*";
					next;
					mes "[Deagle]";
					mes "Ah~ Os velhos dias. Se nós... Se nós não tivessemos entrado naquela TEMPESTADE... *^CCCCCHicks^000000*";
					next;
					mes "[Deagle]";
					mes "AH~ Capitão. Eu sinto falta do nosso capitão mais do que qualquer coisa... Nenhum inimigo sobreviveu diante da espada do nosso capitão.";
					close;
				case 2:
					mes "[Deagle]";
					mes "Isso mesmo! NINGUÉM meche com a tripulação do 'Going Mary!' e ninguém pode derrotar nosso capitão em uma luta de espadas!";
					next;
					mes "[Deagle]";
					mes "CAPITÃO~!!! *^CCCCCHicks~^000000* Ele iria balançar sua espada assim, e então... ENTÃO!!";
					next;
					mes "[Deagle]";
					mes "O bastardo com quem o capitão estava lutando, e todos os amigos perto dele foram cercados em chamas!";
					next;
					mes "[Deagle]";
					mes "Cara, aquela espada devia ter algum tipo de poder misterioso, ou o capitão era apenas muito bom...!";
					next;
					mes "[Deagle]";
					mes "Ufa~~ *^CCCCCCSnif^000000* *^CCCCCCSnif...^000000* Deus, eu sinto falta de todo mundo! Agora estou deprimido! Por favor, vá embora.";
					close;
			}
		case 2:
			mes "[Deagle]";
			mes "Isso Mesmo!";
			mes "Vá EMBORA~";
			close;
	}
}

alberta,58,80,2	script	Shakir	99,{
	mes "[Shakir]";
	if (rand(2)) {
		mes "Nós, mercadores, temos nossas próprias habilidades de negociação quando vendemos mercadorias. Essa habilidade nos dá mais dinheiro que qualquer outra pessoa pode ganhar vendendo mercadorias.";
		next;
		mes "[Shakir]";
		mes "É mais do que apenas gritar: 'Você terá que dar mais dinheiro por favor!', você precisa ser carismático, e mestre em retórica!";
		next;
		mes "[Shakir]";
		mes "Nós podemos conseguir até 24% mais zeny com essa incrível habilidade. Mas lembre-se de treinar duramente para adquirir ela!";
	} else {
		mes "Nós, Mercadores, podemos abrir lojas na estrada para fazer negócios.";
		next;
		mes "[Shakir]";
		mes "Com a habilidade Desconto, podemos comprar mercadorias muito mais baratas nas lojas das cidades, e carregá-las nos carrinhos que alugamos.";
		next;
		mes "[Shakir]";
		mes "Então depois, nos podemos viajar para qualquer lugar, e lucrar vendendo nossas mercadorias!";
		next;
		mes "[Shakir]";
		mes "Desse jeito, os negócios ficam mais práticos e seguros. Não caia no sono, apesar de ser muito fácil fazer isso.";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "Você sabe aqueles ursos preguiçosos que vivem na floresta à caminho de Payon?";
		next;
		mes "[Sonya]";
		mes "Apenas por diversão, eu joguei uma pedra em um e de repente ele correu atrás de mim! Eu fiquei muuuuiito assustada, eu começei a correr, então BAM!!!";
		next;
		mes "[Sonya]";
		mes "Ele correu e bateu num galho baixo de uma árvore e foi nocauteado! Eu juro, eu nunca mais provocarei um animal por diversão de novo!";
		close;
	case 1:
		mes "Ei,sabe, uma vez eu estava andando pela floresta e eu vi um pequeno caule verde se movendo";
		next;
		mes "[Sonya]";
		mes "Fui ver o quê aquilo era, e quando eu fui tocar, o caule bateu na minha mão!";
		next;
		mes "[Sonya]";
		mes "Aquilo me assustou, então eu pulei para trás um pouco. Foi quando eu percebi que não era apenas um caule, mas um animal muito pequeno.";
		next;
		mes "[Sonya]";
		mes "Tive sorte de não aborrecer ele. Mesmo os menores animais podem ser perigosos se irritados.";
		close;
	case 2:
		mes "Eu uma vez vi uma alcatéia atacar um daqueles enormes ursos preguiçosos!";
		next;
		mes "[Sonya]";
		mes "Lobos são muito mais cooperativos do que eles possam parecer. Se um deles é atacado, então qualquer lobo por perto irá correr para ajudar.";
		next;
		mes "[Sonya]";
		mes "Eu pensaria duas vezes se você quiser atacar um quando outros da mesma espécie estão por perto. Seja cuidadoso: não fique no meio de um bando deles!";
		close;
	}
}

alberta,93,174,2	script	Avó Alma	103,{
	mes "[Avó Alma]";
	mes "Algum tempo atrás,um navio abandonado parou no porto de Alberta.";
	next;
	mes "[Avó Alma]";
	mes "Com a intenção de salvar os sobrevivente, alguns habitantes da cidade se aventuraram dentro do navio.Porém, todos eles sairam aterrorizados, dizendo que os corpos estavam andando por todo o lugar dentro do navio.";
	next;
	mes "[Avó Alma]";
	mes "O navio também estava cheio de seres marinhos perigosos, e eles não conseguiram entrar, mesmo se eles quisessem.";
	next;
	mes "[Avó Alma]";
	mes "Nós não pudemos fazer nada sobre aquele navio sinistro, e apenas deixamos ele do jeito que estava. Atualmente, equipes de exploração tentam entrar naquele navio e aniquilar aqueles monstros.";
	next;
	mes "[Avó Alma]";
	mes "Então, deve ser uma boa experiência para uma pessoa jovem como você ser um recruta. Mas, ainda não vale arriscar sua vida se você não for forte o suficiente.";
	close;
}

alberta,189,151,5	script	Fisk	100,{
	mes "[Fisk]";
	mes "Opa parceiro,";
	mes "para onde";
	mes "você quer ir?";
	next;
	switch(select("Navio naufragado -> 250 zeny.","Marina de Izlude -> 500 zeny.","Esqueça.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Fisk]";
				mes "Ei, não tente me enganar! Eu falei 250 zeny!";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Fisk]";
				mes "De nenhum jeito você vai sair daqui sem pagar os 500 zeny primeiro!";
				close;
			}
			set Zeny,Zeny - 500;
			warp "izlude",176,182;
			end;
		case 3:
			mes "[Fisk]";
			mes "Certo...";
			mes "Marinheiro de água doce.";
			close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	100,{
	mes "[Fisk]";
	mes "Então você quer voltar para a ilha principal de Alberta, é?";
	next;
	if(select("Sim, por favor.","Troquei de idéia.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	86,{
	mes "[Paul]";
	mes "Bom dia~";
	mes "Você gostaria";
	mes "de se juntar à";
	mes "equipe de exploração";
	mes "do navio Naufragado?";
	next;
	mes "[Paul]";
	mes "Ah! Antes de se juntar eu devo avisar que se você não é forte o suficiente, você não deveria ir.";
	next;
	mes "[Paul]";
	mes "E então, quer";
	mes "se inscrever?";
	mes "A taxa de admissão";
	mes "é de apenas";
	mes "200 Zeny.";
	next;
	switch(select("Inscrever-me!","Não, obrigado.")) {
		case 1:
			if (Zeny < 200) {
				mes "[Paul]";
				mes "Parece que você não tem o dinheiro meu amigo. Por favor volte quando puder pagar.";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Paul]";
			mes "Certo, Bem...";
			mes "Eu estarei por perto";
			mes "se você mudar";
			mes "de idéia.";
			close;
	}
}

alberta,190,173,4	script	Phelix	85,{
	set .@weight,MaxWeight-Weight;
	mes "[Phelix]";
	if ((.@weight) < 10000) {
		mes "Espere um momento!!";
		mes "Você trouxe muitas coisas!";
		mes "Você não pode aguentar mais nenhum item!";
		mes "Por favor reduza a quantidade de itens,";
		mes "e venha me ver de novo.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "Mas o quê você está fazendo aqui?";
		mes "Não existe nada que você possa pegar de graça nesse navio, se quiser algo, trabalhe para isso!!";
		next;
		mes "[Phelix]";
		mes "Hmm, então por que você não me traz 10 jellopies e eu vou te dar 1 poção. o que você acha?";
		mes "Ou se isso for muito difícil para você, 3 jellopies por 1 Cenoura.";
		next;
		mes "[Phelix]";
		mes "Se você estiver interessado em minha oferta, traga-me os itens que eu mencionei.";
		set @event_zelopy,1;
		close;
	} else {
		mes "Hmm... você quer trocar";
		mes "jellopies por Poções Vermelhas ou  por Cenouras é? Bem... por qual?";
		next;
		switch(select("Poções Vermlhas por favor.","Cenouras por favor.")) {
		case 1:
			mes "[Phelix]";
			mes "Ok...";
			mes "Vamos ver";
			mes "o que você conseguiu...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 10) {
				mes "Ei! você não estava escutando? Eu disse 10 jellopies por 1 Poção Vermelha.. você é surdo?";
				close;
			} else {
				set .@max,countitem(909)/10;
				mes "Hmm, bom...";
				mes "Quantas poções";
				mes "você quer?";
				next;
				switch(select("Quantas eu puder, por favor.","Eu quero essa quantidade.","Não importa, Eu gosto dos meus Jellopies.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;

				case 2:
					mes "[Phelix]";
					mes "Eu não posso dar mais de 100";
					mes "ao mesmo tempo, então não me aborreça Ok? Se não quiser nenhuma, apenas diga '0'.";
					mes "Agora, o máximo que você pode pegar é "+ gap +" mas lembre-se, 100 no máximo, você quer me quebrar minhas costas?";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount <= 0) {
						mes "Muito obrigado, volte quando quiser.";
						close;
					}
					if (.@amount > 100) {
						mes "Ei, O que eu disse? 100 por vez no máximo, você está tentando me matar não é!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Hmmm, parece que você não tem o suficiente. Vá e pegue mais jellopies se quiser alguma coisa de mim.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[Phelix]";
					mes "Sem problemas,";
					mes "vejo você depois.";
					close;
				}
				mes "[Phelix]";
				mes "Aqui vai! Como eu tinha prometido. Não vá tomando elas todas de uma vez.";
				close;
			}
		case 2:
			mes "[Phelix]";
			mes "Certo, Deixe-me ver o que você tem aí...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 3) {
				mes "Hmm, Eu disse 3 jellopies por 1 Cenoura.. entendeu?";
				close;
			} else {
				set .@max,countitem(909)/3;
				mes "Não foi tão ruim...";
				mes "Quantos você quer?";
				next;
				switch(select("Quantas eu puder, por favor.","Eu quero essa quantidade.","Não importa, Eu gosto dos meus Jellopies.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[Phelix]";
					mes "Certo, eu não estou dando mais do que 100 por vez, então não me aborreça, ok? Se não quiser nenhuma, apenas diga '0'.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount == 0) {
						mes "Ok então, vejo você depois.";
						close;
					}
					if (.@amount > 100) {
						mes "Ei, eu falei 100 por vez, não mais que isso! Eu não vou quebrar minhas costas as custas de gente como você!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Parece que você não tem o suficiente. Vá pegar mais se quiser alguma coisa à mais de mim";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[Phelix]";
					mes "Vejo você depois.";
					close;
				}
				mes "[Phelix]";
				mes "Aqui vai~! Como eu prometi. Tente não encher a cara.";
				close;
			}
		}
	}
}