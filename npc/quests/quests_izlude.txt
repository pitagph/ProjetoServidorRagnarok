//===== Cronus Script ========================================
//= Quests de Izlude.
//===== Por: ================================================= 
//= SoulBlaker
//===== Versão Atual: ======================================== 
//= 1.1b CR / 1.1 eA
//===== Compatibilidade: ===================================== 
//= Cronus
//===== Descrição: ===========================================
//= [Aegis conversion]
//= Vars em  uso: MISC_QUEST (Bit 16)
//===== Comentários Adicional: =============================== 
//= 1.0 Initial release [Evera]
//= 1.1 Updated to 10.3 standard. [L0ne_W0lf]
//= 1.1a Traduzido o npc baseado no bRO. [SoulBlaker]
//= 1.1b Revisão do script, em conforme ao bRo [Asus]
//============================================================

// Edgar
//============================================================
izlude,182,186,2	script	Edgar#izlude	49,{
	if (MISC_QUEST & 16) {
		mes "[Edgar]";
		mes "Então que ir para Alberta de novo? Vou cobrá-lo 250 Zenys como da última vez.";
		mes "Que tal?";
		next;
		if (select("Certo:Por que está sendo tão gentil comigo?") == 1) {
			if (Zeny < 250) {
				mes "[Edgar]";
				mes "Você não tem dinheiro suficiente!";
				mes "Vá conseguir mais.";
				close ;
			}
			set zeny,zeny-250;
			warp "alberta",195,164;
			end;
		}
		mes "[Edgar]";
		mes "É apenas o meu jeito de ser. Isso, e seu sorriso malicioso me faz lembrar de meu amado filho de cabelos loiros, que saiu de casa há anos para virar um marinheiro em seu próprio barco. Abençoada seja sua alma, onde estiver.";
		close;
	}
	mes "[Edgar]";
	mes "Minha cidade, Izlude, está";
	mes "conectada a Alberta pelo";
	mes "ancoradouro a Oeste. Há muito";
	mes "tráfego entre as cidades, então eu";
	mes "quase sou de Alberta e não de Izlude. Hehehe.";
	mes "Eu conheço muito bem aquele sujeito";
	mes "Phelix de Alberta, aquele cara é";
	mes "muito exigente, então é sem almoço grátis com ele.";
	next;
	mes "[Edgar]";
	mes "Mas ele é um cara legal, ele gosta";
	mes "de ajudar aos outros. Ele tem um";
	mes "grande coração, ele te ajudará";
	mes "bastante se você atender as";
	mes "condições que ele pede.";
	next;
	mes "[Edgar]";
	mes "Ultimamente as pessoas de Alberta";
	mes "dizem que ele vem ajudando pessoas";
	mes "que lhe trazem uma grande";
	mes "quantidade de Jellopys ou algo";
	mes "parecido.. se vocês estiver com o";
	mes "bolso vazio, é melhor dar uma";
	mes "olhada no que ele quer.";
	next;
	if (select("Pode me dizer o caminho para Alberta?:Fim da conversa") == 1) {
		mes "[Edgar]";
		mes "Como? Você pode ir andando, mas se";
		mes "você tiver dinheiro eu sugiro que";
		mes "você pegue um barco.";
		next;
		if (select("Ok entendi.:Cansado de andar e sem dinheiro.") == 1 ) {
			mes "[Edgar]";
			mes "Tudo bem, cuide-se...";
			close;
		}
		mes "[Edgar]";
		mes "Cansado de andar e sem dinheiro?";
		mes "Meu amigo..";
		mes "Certo, certo, como capitão do navio";
		mes "eu posso te levar a um preço menor, 250 Zeny.";
		next;
		if (select("Legal!!!:Que extorsão!!") == 1) {
			set MISC_QUEST,MISC_QUEST | 16;
			if (Zeny < 250) {
				mes "[Edgar]";
				mes "Mas você não tem dinheiro o";
				mes "suficiente! Vá conseguir mais.";
				close;
			}
			set zeny,zeny-250;
			warp "alberta",195,164;
			end;
		}
		mes "[Edgar]";
		mes "Garoto, ah Garoto,";
		mes "se você acha melhor";
		mes "vá dar uma volta...";
		close;
	}
	mes "[Edgar]";
	mes "Yeah, tudo bem.";
	mes "Vejo você por aí~";
	close;
}